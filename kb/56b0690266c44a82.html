<html>
<head>
  <title>Эффект волн (синусоидальные, вид сбоку)</title>
  <meta http-equiv="Content-Type" content="text/html; charset=windows-1251" />
  <link type="text/css" href="css/css.css" rel="stylesheet" />
  <link type="text/css" href="css/sh.css" rel="stylesheet" />
  <script language="javascript" src="js/shInit.js"></script>
  <script language="javascript" src="js/shCore.js"></script>
  <script language="javascript" src="js/shBrushDelphi.js"></script>
  <script language="javascript" src="js/shBrushSql.js"></script>
</head>
<body>
<div id="layout">
<div id="header">
  <div id="logo"><img id="logo" src="img/logo.png" /></div>
  <div id="navigation">
    <p id="navigation">
      <a class="navigation" onclick="prev();" href="#">Предыдущая</a><br />
      <a class="navigation" onclick="up();" href="#">Наверх</a><br />
      <a class="navigation" onclick="next();" href="#">Следующая</a>
    </p>
  </div>
</div>
<div id="content">
<div id="explorer">DRKB Explorer</div>
  <h1 id="title">Эффект волн (синусоидальные, вид сбоку)</h1>
<div id="date">01.01.2010</div>
<!-- Actual content start -->

<pre name="code" class="delphi">
{ **** UBPFD *********** by delphibase.endimus.com ****
&gt;&gt; Эффект 'Волны' (синусоидальные, вид сбоку)
 
Зависимости: Classes, Graphics
Автор:       Fenik, chook_nu@uraltc.ru, Новоуральск
Copyright:   Собственное написание (Николай федоровских)
Дата:        1 июня 2002 г.
***************************************************** }
 
procedure WaveSin(Bitmap: TBitmap; Frequency, Length:
  Integer; Hor: Boolean; BackColor: TColor);
 
  function Min(A, B: Integer): Integer;
  begin
    if A &lt; B then
      Result := A
    else
      Result := B;
  end;
 
  function Max(A, B: Integer): Integer;
  begin
    if A &gt; B then
      Result := A
    else
      Result := B;
  end;
 
const
  Rad = Pi / 180;
type
  TRGB = record
    B, G, R: Byte;
  end;
  pRGB = ^TRGB;
var
  x, y, f: Integer;
  Dest, Src: pRGB;
  Bmp: TBitmap;
begin
  Bitmap.PixelFormat := pf24Bit;
  Bmp := TBitmap.create;
  try
    Bmp.Assign(Bitmap);
    Bitmap.Canvas.Brush.Color := BackColor;
    Bitmap.Canvas.FillRect(Rect(0, 0, Bitmap.Width, Bitmap.Height));
    for y := 0 to Bmp.Height - 1 do
    begin
      Src := Bmp.ScanLine[y];
      for x := 0 to Bmp.Width - 1 do
      begin
        if Hor then
        begin
          f := Min(Max(Round(Sin(x * Rad * Length) * Frequency) + y, 0),
            Bitmap.Height - 1);
          Dest := Bitmap.ScanLine[f];
          Inc(Dest, x);
        end
        else
        begin
          f := Min(Max(Round(Sin(y * Rad * Length) * Frequency) + x, 0),
            Bitmap.Width - 1);
          Dest := Bitmap.ScanLine[y];
          Inc(Dest, f);
        end;
        Dest^ := Src^;
        Inc(Src);
      end;
    end;
  finally
    Bmp.free;
  end;
end;
Пример использования: 
 
WaveSin(FBitmap, FBitmap.Width div 50, FBitmap.Width div 40, True, clWhite); 
 
</pre>

</div>
<!-- Actual content end -->
<hr />
<div id="footer">
<p>&copy; DRKB Library, 2010<br />Разработка и поддержка &mdash; <a href="http://www.drkb.ru/" target="_blank">Quadr0</a></p>
</div>
</div>
</body>
</html>
