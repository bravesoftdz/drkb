<html>
<head>
  <title>Drag &amp; Drop в TTreeView</title>
  <meta http-equiv="Content-Type" content="text/html; charset=windows-1251" />
  <link type="text/css" href="css/css.css" rel="stylesheet" />
  <link type="text/css" href="css/sh.css" rel="stylesheet" />
  <script language="javascript" src="js/shInit.js"></script>
  <script language="javascript" src="js/shCore.js"></script>
  <script language="javascript" src="js/shBrushDelphi.js"></script>
  <script language="javascript" src="js/shBrushSql.js"></script>
</head>
<body>
<div id="layout">
<div id="header">
  <div id="logo"><img id="logo" src="img/logo.png" /></div>
  <div id="navigation">
    <p id="navigation">
      <a class="navigation" onclick="prev();" href="#">Предыдущая</a><br />
      <a class="navigation" onclick="up();" href="#">Наверх</a><br />
      <a class="navigation" onclick="next();" href="#">Следующая</a>
    </p>
  </div>
</div>
<div id="content">
<div id="explorer">DRKB Explorer</div>
  <h1 id="title">Drag &amp; Drop в TTreeView</h1>
<div id="date">01.01.2010</div>
<!-- Actual content start -->

<pre name="code" class="delphi">
unit Unit1;
 
interface
 
uses
  Windows, Messages, SysUtils, Variants, Classes, Graphics, Controls, Forms,
  ComCtrls;
 
type
  TForm1 = class(TForm)
    TreeView1: TTreeView;
    procedure TreeView1DragDrop(Sender, Source: TObject; X, Y: Integer);
    procedure TreeView1DragOver(Sender, Source: TObject; X, Y: Integer;
      State: TDragState; var Accept: Boolean);
  private
    procedure MoveNode(TargetNode, SourceNode: TTreeNode);
  end;
 
var
  Form1: TForm1;
 
implementation
 
{$R *.dfm}
 
procedure TForm1.MoveNode(TargetNode, SourceNode: TTreeNode);
var
  NodeTmp: TTreeNode;
  I: Integer;
begin
  with TreeView1 do
  begin
    // проверяем, является ли целевой элемент предком перетаскиваемого
    NodeTmp := TargetNode.Parent;
    while Assigned(NodeTmp) do
      if NodeTmp = SourceNode then
        Abort
      else
        NodeTmp := NodeTmp.Parent;
 
    // копируем перетаскиваемый элемент в новосозданный
    NodeTmp := Items.AddChild(TargetNode, SourceNode.Text);
    NodeTmp.Data := SourceNode.Data;
 
    for I := 0 to SourceNode.Count - 1 do
      MoveNode(NodeTmp, SourceNode.Item[I]);
 
    Selected := NodeTmp;
    TopItem := NodeTmp.getPrev;
    TargetNode.Expand(True);
  end;
end;
 
procedure TForm1.TreeView1DragDrop(Sender, Source: TObject; X, Y: Integer);
var
  TargetNode, SourceNode: TTreeNode;
begin
  with TreeView1 do
  begin
    TargetNode := GetNodeAt(X, Y);
    SourceNode := Selected;
 
    if (TargetNode = SourceNode) or (TargetNode = nil) then
    begin
      EndDrag(False);
      Exit;
    end;
    MoveNode(TargetNode, SourceNode);
    SourceNode.Free;
  end;
end;
 
procedure TForm1.TreeView1DragOver(Sender, Source: TObject; X, Y: Integer;
  State: TDragState; var Accept: Boolean);
var
  SourceNode, TargetNode, NodeTmp: TTreeNode;
begin
  if Sender = TreeView1 then
  try
    // скроллинг дерева при перетаскивании
    if Y &gt; TreeView1.Height - 10 then
    begin
      TreeView1.TopItem := TreeView1.TopItem.getNext;
      Sleep(100); // пауза
    end else
      if Y &lt; 10 then
      begin
        TreeView1.TopItem := TreeView1.TopItem.getPrev;
        Sleep(100); // пауза
      end;
 
    TargetNode := TreeView1.GetNodeAt(X, Y);
    SourceNode := TreeView1.Selected;
 
    if (TargetNode = nil) or (TargetNode = SourceNode) then Abort;
 
    Accept := True;
    // проверяем, является ли целевой элемент предком перетаскиваемого
    NodeTmp := TargetNode.Parent;
    while Assigned(NodeTmp) do
      if NodeTmp = SourceNode then
        Abort
      else
        NodeTmp := NodeTmp.Parent;
  except
    Accept := False;
  end;
end;
 
end.
</pre>
<p id="author">Автор: Smike</p>
<p>Взято из <a href="http://forum.sources.ru" target="_blank">http://forum.sources.ru</a></p>
<hr />
<pre name="code" class="delphi">
unit Unit1;
 
 interface
 
 uses
   Windows, Messages, SysUtils, Variants, Classes, Controls, Forms,
   Dialogs, StdCtrls, ComCtrls;
 
 type
   TForm1 = class(TForm)
     TreeView1: TTreeView;
     Label1: TLabel;
     procedure TreeView1DragDrop(Sender, Source: TObject; X, Y: Integer);
     procedure TreeView1DragOver(Sender, Source: TObject; X, Y: Integer;
       State: TDragState; var Accept: Boolean);
   private
     { Private declarations }
     Procedure MoveNode(TargetNode, SourceNode : TTreeNode);
   public
     { Public declarations }
   end;
 
 var
   Form1: TForm1;
 
 implementation
 
 {$R *.dfm}
 
 // 
// Procedure which will move node and its subnodes 
// 
Procedure TForm1.MoveNode(TargetNode, SourceNode : TTreeNode);
 var
   nodeTmp : TTreeNode;
   i : Integer;
 begin
   with TreeView1 do
   begin
     nodeTmp := Items.AddChild(TargetNode,SourceNode.Text);
     for i := 0 to SourceNode.Count -1 do
     begin
       MoveNode(nodeTmp,SourceNode.Item[i]);
     end;
   end;
 end;
 
 
 procedure TForm1.TreeView1DragDrop(Sender, Source: TObject; X, Y: Integer);
 var
   TargetNode, SourceNode : TTreeNode;
 begin
   with TreeView1 do
   begin
     TargetNode := GetNodeAt(X,Y); // Get target node 
    SourceNode := Selected;
     if (TargetNode = nil) then
     begin
       EndDrag(False);
       Exit;
     end;
     MoveNode(TargetNode,SourceNode);
     SourceNode.Free;
   end;
 end;
 
 procedure TForm1.TreeView1DragOver(Sender, Source: TObject; X, Y: Integer;
   State: TDragState; var Accept: Boolean);
 begin
   if (Sender = TreeView1) then // If TRUE than accept the draged item 
  begin
     Accept := True;
   end;
 end;
 
 end.
</pre>
<p>Взято с сайта: <a href="http://www.swissdelphicenter.ch" target="_blank">http://www.swissdelphicenter.ch</a></p>
&nbsp;</p>
</div>
<!-- Actual content end -->
<hr />
<div id="footer">
<p>&copy; DRKB Library, 2010<br />Разработка и поддержка &mdash; <a href="http://www.drkb.ru/" target="_blank">Quadr0</a></p>
</div>
</div>
</body>
</html>
