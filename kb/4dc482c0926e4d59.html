<html>
<head>
  <title>Как поместить TProgressBar в панель StatusBar?</title>
  <meta http-equiv="Content-Type" content="text/html; charset=windows-1251" />
  <link type="text/css" href="css/css.css" rel="stylesheet" />
  <link type="text/css" href="css/sh.css" rel="stylesheet" />
  <script language="javascript" src="js/shInit.js"></script>
  <script language="javascript" src="js/shCore.js"></script>
  <script language="javascript" src="js/shBrushDelphi.js"></script>
  <script language="javascript" src="js/shBrushSql.js"></script>
</head>
<body>
<div id="layout">
<div id="header">
  <div id="logo"><img id="logo" src="img/logo.png" /></div>
  <div id="navigation">
    <p id="navigation">
      <a class="navigation" onclick="prev();" href="#">Предыдущая</a><br />
      <a class="navigation" onclick="up();" href="#">Наверх</a><br />
      <a class="navigation" onclick="next();" href="#">Следующая</a>
    </p>
  </div>
</div>
<div id="content">
<div id="explorer">DRKB Explorer</div>
  <h1 id="title">Как поместить TProgressBar в панель StatusBar?</h1>
<div id="date">01.01.2010</div>
<!-- Actual content start -->

<p>Корректнее было бы самому канву рисовать, но можно и просто вставить - держи функцию для этого - применять вместо стандартного метода Create.</p>
<pre name="code" class="delphi">
Function CreateProgressBar(StatusBar:TStatusBar; index:integer):TProgressBar;
  var findleft:integer;
      i:integer;

 
begin
  result:=TProgressBar.create(Statusbar);
  result.parent:=Statusbar;
  result.visible:=true;
  result.top:=2;
  findleft:=0;
  for i:=0 to index-1 do findleft:=findleft+Statusbar.Panels[i].width+1;
  result.left:=findleft;
  result.width:=Statusbar.Panels[index].width-4;
  result.height:=Statusbar.height-2;
end;
</pre>

<p id="author">Автор: Vit</p>
<p>Взято с Vingrad.ru <a href="http://forum.vingrad.ru" target="_blank">http://forum.vingrad.ru</a></p>
<hr /><p>Есть два принципиально разных решения. Первый вариант - это сделать все " вручную" .</p>
<p>Здесь создается Bitmap с текстом (возможно любое изображение). Чтобы нарисовать светлую часть полосы, достаточно скопировать кусок Bitmap на StatusBar, а чтобы нарисовать темную часть полосы, нужно скопировать кусок Bitmap с инвертированием. При этом фон станет темным, а текст светлым. Реализация ясна из самой программы.</p>
<p>Второй вариант более простой в реализации, но и менее функциональный. StatusBar является наследником TWinControl, а следовательно, на нем можно разместить еще какие-то компоненты. Но сделать это можно только динамически (непосредственно из программы). На StatusBar помещается компонент ProgressBar, вначале невидимый. Когда в нем появляется необходимость, его нужно сделать видимым и начать изменять свойство Position.</p>
<p>Из этого примера хорошо видны некоторые достоинства и недостатки объектов.</p>
<p>Если у Вас Delphi3, то строчка pb.Smooth := true; работать не будет. На сайте выложена версия программы с заменой этой строчки. Впрочем, ее можно просто удалить - принципиально это ничего не изменит. Скачать все необходимые для компиляции файлы проекта можно на program.dax.ru.</p>
<p>Способ 1</p>
<pre name="code" class="delphi">
uses Commctrl;
const
 MaxProgress = 50;
var
 bm: TBitmap;
// Возвращает прямоугольник нулевой панели:
function GetPanelRect: TRect;
begin
 SendMessage(Form1.StatusBar1.Handle, SB_GETRECT, 0,
   integer(@result));
 InflateRect(result, -1, -1);
end;
 
// Копирует часть bm на StatusBar
procedure CopyPart(left, right: integer; ACopyMode: TCopyMode);
var bmRect, pnRect: TRect;
begin
 bmRect := Rect(left, 0, right, bm.Height - 1);
 pnRect := bmRect;
 with GetPanelRect do
   OffsetRect(pnRect, Left, Top);
 with Form1.StatusBar1.Canvas do begin
   CopyMode := ACopyMode;
   CopyRect(pnRect, bm.Canvas, bmRect);
 end;
end;
 
procedure TForm1.FormCreate(Sender: TObject);
begin
 with StatusBar1.Panels.Add do begin
   Width := 100;
   Style := psOwnerDraw;
 end;
 with StatusBar1.Panels.Add do begin
   Width := 0;
   Text := 'abc';
 end;
 Timer1.Enabled := false;
 Timer1.Interval := 50;
end;
 
procedure TForm1.Button1Click(Sender: TObject);
begin
 Timer1.Enabled := true;
 bm := TBitmap.Create;
 with GetPanelRect do begin
   bm.Width := Right - Left;
   bm.Height := Bottom - Top;
 end;
 with bm.Canvas do begin
   Brush.Color := clSilver;
   FillRect(Bounds(0, 0, bm.Width, bm.Height));
   TextOut(1, 1, 'Doing smth...');
 end;
 CopyPart(0, bm.Width - 1, cmSrcCopy); // Вывод текста
end;
 
procedure TForm1.Timer1Timer(Sender: TObject);
begin
 Timer1.Tag := Timer1.Tag + 1;
 if Timer1.Tag &gt;  MaxProgress then begin
   Timer1.Enabled := false;
   Timer1.Tag := 0;
   StatusBar1.Repaint; // Очистка StatusBar
 end else
   // Вывод только что закрашенной части:
   CopyPart(trunc((Timer1.Tag - 1) / MaxProgress * bm.Width),
     trunc(Timer1.Tag / MaxProgress * bm.Width), cmNotSrcCopy);
end;
 
procedure TForm1.StatusBar1DrawPanel(StatusBar: TStatusBar;
 Panel: TStatusPanel; const Rect: TRect);
var p: integer;
begin
 if (Panel.Index = 0) and (Timer1.Tag &gt;  0) then begin
   p := round((Rect.Right - Rect.Left) * Timer1.Tag / MaxProgress);
   // Вывод закрашенной части:
   CopyPart(0, p, cmNotSrcCopy);
   // Вывод незакрашенной части:
   CopyPart(p + 1, bm.Width - 1, cmSrcCopy);
 end;
end;
</pre>
<p>Способ 2</p>
<pre name="code" class="delphi">
uses Commctrl;
const
 MaxProgress = 50;
var pb: TProgressBar;
 
function GetPanelRect: TRect;
begin
 SendMessage(Form1.StatusBar1.Handle, SB_GETRECT, 0, integer(@result));
end;
 
procedure TForm1.FormCreate(Sender: TObject);
begin
 with StatusBar1.Panels.Add do begin
   Width := 100;
   Style := psOwnerDraw;
 end;
 with StatusBar1.Panels.Add do begin
   Width := 0;
   Text := 'abc';
 end;
 Timer1.Enabled := false;
 Timer1.Interval := 50;
 pb := TProgressBar.Create(StatusBar1);
 pb.Visible := false;
 pb.Parent := StatusBar1;
 pb.BoundsRect := GetPanelRect;
 pb.Smooth := true;
 pb.Step := 1;
end;
 
procedure TForm1.Button1Click(Sender: TObject);
begin
 Timer1.Enabled := true;
 pb.Position := 0;
 pb.Max := MaxProgress;
 pb.Visible := true;
end;
 
procedure TForm1.Timer1Timer(Sender: TObject);
begin
 Timer1.Tag := Timer1.Tag + 1;
 if Timer1.Tag &gt;  MaxProgress then begin
   Timer1.Enabled := false;
   Timer1.Tag := 0;
   pb.Visible := false;
 end else pb.StepIt;
end;
</pre>

<p>Даниил Карапетян.</p>
<p>На сайте <a href="http://delphi4all.narod.ru" target="_blank">http://delphi4all.narod.ru</a> Вы найдете еще более 100 советов по Delphi.</p>
<p>Email: <a href="mailto:delphi4all@narod.ru" target="_blank">delphi4all@narod.ru</a></p>
</div>
<!-- Actual content end -->
<hr />
<div id="footer">
<p>&copy; DRKB Library, 2010<br />Разработка и поддержка &mdash; <a href="http://www.drkb.ru/" target="_blank">Quadr0</a></p>
</div>
</div>
</body>
</html>
