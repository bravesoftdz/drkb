<html>
<head>
  <title>Использование TWebBrowser (статья)</title>
  <meta http-equiv="Content-Type" content="text/html; charset=windows-1251" />
  <link type="text/css" href="css/css.css" rel="stylesheet" />
  <link type="text/css" href="css/sh.css" rel="stylesheet" />
  <script language="javascript" src="js/shInit.js"></script>
  <script language="javascript" src="js/shCore.js"></script>
  <script language="javascript" src="js/shBrushDelphi.js"></script>
  <script language="javascript" src="js/shBrushSql.js"></script>
</head>
<body>
<div id="layout">
<div id="header">
  <div id="logo"><img id="logo" src="img/logo.png" /></div>
  <div id="navigation">
    <p id="navigation">
      <a class="navigation" onclick="prev();" href="#">Предыдущая</a><br />
      <a class="navigation" onclick="up();" href="#">Наверх</a><br />
      <a class="navigation" onclick="next();" href="#">Следующая</a>
    </p>
  </div>
</div>
<div id="content">
<div id="explorer">DRKB Explorer</div>
  <h1 id="title">Использование TWebBrowser (статья)</h1>
<div id="date">01.01.2010</div>
<!-- Actual content start -->

Использование TWebBrowser</p>
<p>Во многих современных программах требуется работа с данными в формате HTML. В качестве средства для просмотра таких данных в Delphi используется ActiveX компонент TWebBrowser, который использует компонент WebBrowser, входящий в состав Microsoft Internet Explorer. Таким образом, он имеется на любом компьютере, на котором установлен Internet Explorer. Все последние версии Windows включают этот компонент в стандартной поставке и, более того, практически неработоспособны без него.</p>
Базовые операции</p>
<p>Для того, чтобы использовать TWebBrowser в своей программе необходимо разместить на форме соответствующий компонент, размещенный на закладке Internet. После этого, чтобы отобразить в нем страницу HTML, необходимо вызвать его метод Navigate</p>
<pre name="code" class="delphi">
procedure TForm1.Button1Click(Sender: TObject);
var
  Flags, TargetFrameName, PostData, Headers: OleVariant;
begin
  WebBrowser1.Navigate('http://www.borland.com', Flags, 
    TargetFrameName, PostData, Headers);
end;
</pre>

<p>Рассмотрим подробнее параметры, передаваемые в метод Navigate. </p>
<p>Первым параметром передается строка с URL, указывающим адрес, из которого должна осуществляться загрузка. Поддерживаются все протоколы, доступные в IE, например file:// - загрузка файла, res:// - загрузка из ресурса.</p>
<p>Остальные параметры не являются обязательными и служат для передачи дополнительной информации.</p>
<p>Flags &nbsp; &nbsp; &nbsp; &nbsp;Целое число, представляющее из себя битовую маску из следующих флагов: 1&nbsp; Открыть ресурс в новом окне 2&nbsp; Не добавлять страницу в историю просмотренных 4&nbsp; Не загружать страницу из кэша 8&nbsp; Не сохранять страницу в кэше &nbsp; &nbsp; &nbsp; &nbsp;</p>
<p>TargetFrameName &nbsp; &nbsp; &nbsp; &nbsp;Задает имя фрейма, в который будет загружена страница &nbsp; &nbsp; &nbsp; &nbsp;</p>
<p>PostData &nbsp; &nbsp; &nbsp; &nbsp;Задает данные для запроса с сервера методом HTTP POST. Если этот параметр пустой &#8211; используется метод GET &nbsp; &nbsp; &nbsp; &nbsp;</p>
<p>Headers &nbsp; &nbsp; &nbsp; &nbsp;Задает дополнительные заголовки HTTP &nbsp; &nbsp; &nbsp; &nbsp;</p>
<p>Наиболее интересным является параметр PostData, позволяющий передать на Web-Server данные, полученные в результате заполнения формы, если этот сервер требует HTTP &#8211; транзакции POST. Например, следующий фрагмент кода передает на сервер имя пользователя и пароль, заполненные в форме Delphi</p>
<pre name="code" class="delphi">
var
  LoginDialog: TLoginDialog;
  Flags, TargetFrameName, PostData, Headers: OleVariant;
  S: String;
 
...
with TLoginDialog.Create(Application) do 
try
  if ShowModal = mrOk then 
  begin
    S := Format('UserName=%s&amp;Password=%s', [Edit1.Text, Edit2.Text]);
    PostData := VarArrayCreate([1, Length(S) + 1], varByte);
    System.Move(S[1], VarArrayLock(PostData)^, Length(S) + 1);
    VarArrayUnlock(PostData);
    Headers := 
      'Content-Type: application/x-www-form-urlencoded'#10#13;
    WebBrowser1.Navigate('http://intranetserver/secretpage', Flags,
      TargetFrameName, PostData, Headers);
  end;
finally
  Free;
end;
</pre>

<p>На Web-сервере этот запрос может быть обработан, например, следующим ASP &#8211; скриптом:</p>
<pre name="code" class="delphi">  Dim sConnect
  Dim sUserName
  Dim sPassword
  sUserName = Request.Form("User")
  sPassword = Request.Form("Pass"
  sConnect = "Provider=SQLOLEDB.1;Persist Security Info=True;" &amp; _
             "Initial Catalog=Katren;Data Source=DBSERVER;" &amp; _
             "Password=" &amp; sPassword &amp; _
             ";User ID=" &amp; sUserName
  Session("ConnectString") = sConnect
</pre>

<p>После того как данные получены, необходимо дать пользователю возможность для работы с ними. Многие функции TWebBrowser доступны через метод ExecWB, предоставляющий простой способ обращения к интерфейсу IOleCommandTarget.</p>
<p>Этот метод имеет вид:</p>
<pre name="code" class="delphi">
procedure TWebBrowser.ExecWB(
  cmdID: OLECMDID;            // идентификатор команды
  cmdexecopt: OLECMDEXECOPT;  // параметры выполнения
  var pvaIn,                  // дополнительные параметры,
  pvaOut: OleVariant          // зависящие от команды
); safecall;
</pre>

<p>CmdID может быть одной из констант OLECMDID, определенных в файле ShDocVw.pas.</p>
<p>Параметр cmdexecopt может принимать одно из следующих четырех значений:</p>
<p>OLECMDEXECOPT_DODEFAULT &nbsp; &nbsp; &nbsp; &nbsp;Выполнить команду с настройками «по умолчанию» &nbsp; &nbsp; &nbsp; &nbsp;</p>
<p>OLECMDEXECOPT_PROMPTUSER &nbsp; &nbsp; &nbsp; &nbsp;Запросить у пользователя настройки для выполнения команды (например, при печати &#8211; вывести диалог Print Setup) &nbsp; &nbsp; &nbsp; &nbsp;</p>
<p>OLECMDEXECOPT_DONTPROMPTUSER &nbsp; &nbsp; &nbsp; &nbsp;Выполнить команду не запрашивая пользователя &nbsp; &nbsp; &nbsp; &nbsp;</p>
<p>OLECMDEXECOPT_SHOWHELP &nbsp; &nbsp; &nbsp; &nbsp;Вывести справку о команде &nbsp; &nbsp; &nbsp; &nbsp;</p>
<p>Параметры pvaIn и pvaOut &#8211; дополнительные и зависят от конкретной команды.</p>
<p>Имеется возможность запросить у TWebBrowser доступность той или иной команды при помощи функции:</p>
<p>function TWebBrowser.QueryStatusWB(</p>
<p>  cmdID: OLECMDID&nbsp;&nbsp; // идентификатор команды</p>
<p>): OLECMDF; safecall;</p>
<p>Функция возвращает битовую маску из следующих значений:</p>
<p>OLECMDF_SUPPORTED &nbsp; &nbsp; &nbsp; &nbsp;Команда поддерживается &nbsp; &nbsp; &nbsp; &nbsp;</p>
<p>OLECMDF_ENABLED &nbsp; &nbsp; &nbsp; &nbsp;Команда поддерживается и разрешена &nbsp; &nbsp; &nbsp; &nbsp;</p>
<p>OLECMDF_LATCHED &nbsp; &nbsp; &nbsp; &nbsp;Команда &#8211; переключатель и сейчас включена &nbsp; &nbsp; &nbsp; &nbsp;</p>
<p>OLECMDF_NINCHED &nbsp; &nbsp; &nbsp; &nbsp;Зарезервировано &nbsp; &nbsp; &nbsp; &nbsp;</p>
<p>Таким образом, можно настраивать интерфейс, в зависимости от поддерживаемых текущей версией TWebBrowser возможностей:</p>
<pre name="code" class="delphi">
var
  Flags: OLECMDF;
 
...
 
Flags := WebBrowser1.QueryStatusWB(OLECMDID_COPY);
ActionCopy.Visible := (Flags and OLECMDF_SUPPORTED) = 
  OLECMDF_SUPPORTED;
ActionCopy.Enabled := (Flags and OLECMDF_ENABLED) = 
  OLECMDF_ENABLED;
</pre>

<p>Для печати содержимого TWebBrowser служит команда OLECMDID_PRINT. Метод печати, может выглядеть, например, следующим образом:</p>
<pre name="code" class="delphi">
procedure TForm1.ActionPrintExecute(Sender: TObject);
var
  A, B: OleVariant;
  UserAction: Cardinal;
begin
  if Sender = ActionPrintWithSetup then
    UserAction := OLECMDEXECOPT_PROMPTUSER
  else
    UserAction := OLECMDEXECOPT_DONTPROMPTUSER;
  try
    WebBrowser1.ExecWB(OLECMDID_PRINT, UserAction, A, B);
  except
  end;
end;
</pre>

<p>Блок try … except … end необходим, поскольку TWebBrowser при выполнении любой команды при помощи ExecWB генерирует исключение EOleException с кодом:</p>
<p>-2147221248 ($80040100) Trying to revoke a drop target that has not been registered.</p>
<p>Начиная с Internet Explorer 5 документированы дополнительные команды, поддерживаемые через интерфейс IOleCommandTarget. Они существенно расширяют возможности по управлению компонентом, однако недоступны, либо не документированы в версии 4. Это создает определенные сложности при программировании. Например, чтобы организовать поиск внутри загруженной страницы необходим следующий код:</p>
<pre name="code" class="delphi">
const
  // Недокументированная константа
  CGID_IE4: TGUID = '{ed016940-bd5b-11cf-ba4e-00c04fd70816}';
  // Документировано в IE5 SDK
  CGID_MSHTML: TGUID = '{DE4BA900-59CA-11CF-9592-444553540000}';
  IDM_FIND = 67;
 
procedure TForm1.ActionFindExecute(Sender: TObject);
var
  A, B: OleVariant;
  Target: IOleCommandTarget;
  OleCmd: TOLECMD;
begin
  // Получаем интерфейс IOleCommandTarget
  Target := wbMain.Document as IOLECommandtarget;
  with OleCmd do
  begin
    cmdId := IDM_FIND;
    cmdf  := 0;
  end;
  // Запрашиваем, поддерживается ли команда
  Target.QueryStatus(@CGID_MSHTML, 1, @OleCmd, NIL);
  if (OleCmd.cmdf and OLECMDF_SUPPORTED) = OLECMDF_SUPPORTED then
    // Да, у нас IE5+ - вызываем документированным способом
    Target.Exec(@CGID_MSHTML, IDM_FIND, 
      OLECMDEXECOPT_DODEFAULT, A, B)
  else
    // Нет, у нас IE4 - вызываем недокументированным способом
    Target.Exec(@CGID_IE4, 1, OLECMDEXECOPT_DODEFAULT, A, B);
end;
</pre>

<p>Использование недокументированного вызова в данном случае оправдано, т.к. в версии 4 этот вызов уже не будет изменяться, а в версию 5 мы обнаруживаем и используем документированный метод. В то же время IE4 еще достаточно распространен и совсем лишать программу возможности поиска на таких компьютерах не оправдано. </p>
Тонкая настройка</p>
<p>Если необходима более тонкая настройка компонента &#8211; необходимо реализовать интерфейс IDocHostUIHandler, позволяющий программисту взять под контроль поведение TWebBrowser.</p>
<p>Интерфейс объявлен как:</p>
<pre name="code" class="delphi">
type
  TDocHostInfo = packed record
    cbSize: ULONG;
    dwFlags: DWORD;
    dwDoubleClick: DWORD;
  end;
 
const
  DOCHOSTUIFLAG_DIALOG = 1;
  DOCHOSTUIFLAG_DISABLE_HELP_MENU = 2;
  DOCHOSTUIFLAG_NO3DBORDER = 4;
  DOCHOSTUIFLAG_SCROLL_NO = 8;
  DOCHOSTUIFLAG_DISABLE_SCRIPT_INACTIVE = 16;
  DOCHOSTUIFLAG_OPENNEWWIN = 32;
  DOCHOSTUIFLAG_DISABLE_OFFSCREEN = 64;
  DOCHOSTUIFLAG_FLAT_SCROLLBAR = 128;
  DOCHOSTUIFLAG_DIV_BLOCKDEFAULT = 256;
  DOCHOSTUIFLAG_ACTIVATE_CLIENTHIT_ONLY = 512;
 
const
  DOCHOSTUIDBLCLK_DEFAULT = 0;
  DOCHOSTUIDBLCLK_SHOWPROPERTIES = 1;
  DOCHOSTUIDBLCLK_SHOWCODE = 2;
type
  IDocHostUIHandler = interface(IUnknown)
    ['{bd3f23c0-d43e-11cf-893b-00aa00bdce1a}']
    function ShowContextMenu(const dwID: DWORD; const ppt: PPOINT;
      const pcmdtReserved: IUnknown; 
      const pdispReserved: IDispatch): HRESULT; stdcall;
    function GetHostInfo(var pInfo: TDOCHOSTUIINFO): HRESULT; 
      stdcall;
    function ShowUI(const dwID: DWORD; 
      const pActiveObject: IOleInPlaceActiveObject;
      const pCommandTarget: IOleCommandTarget; 
      const pFrame: IOleInPlaceFrame;
      const pDoc: IOleInPlaceUIWindow): HRESULT; stdcall;
    function HideUI: HRESULT; stdcall;
    function UpdateUI: HRESULT; stdcall;
    function EnableModeless(const fEnable: BOOL): HRESULT; stdcall;
    function OnDocWindowActivate(const fActivate: BOOL): HRESULT; 
      stdcall;
    function OnFrameWindowActivate(const fActivate: BOOL): HRESULT; 
      stdcall;
    function ResizeBorder(const prcBorder: PRECT;
      const pUIWindow: IOleInPlaceUIWindow;
      const fRameWindow: BOOL): HRESULT; stdcall;
    function TranslateAccelerator(const lpMsg: PMSG; 
      const pguidCmdGroup: PGUID;
      const nCmdID: DWORD): HRESULT; stdcall;
    function GetOptionKeyPath(var pchKey: POLESTR; 
      const dw: DWORD): HRESULT; stdcall;
    function GetDropTarget(const pDropTarget: IDropTarget;
      out ppDropTarget: IDropTarget): HRESULT; stdcall;
    function GetExternal(out ppDispatch: IDispatch): HRESULT; 
      stdcall;
    function TranslateUrl(const dwTranslate: DWORD; 
      const pchURLIn: POLESTR; var ppchURLOut: POLESTR): HRESULT;
      stdcall;
    function FilterDataObject(const pDO: IDataObject;
      out ppDORet: IDataObject): HRESULT; stdcall;
  end; 
</pre>

<p>Наследник TWebBrowser, реализующий этот интерфейс должен быть объявлен как</p>
<pre name="code" class="delphi">
type
  TCustomizedWebBrowser = class(TWebBrowser, IDocHostUIHandler)
    // Реализация методов IDocHostUIHandler
  end;
</pre>

<p>Код такого компонента, реализующего минимальную функциональность приведен на CD-ROM. Вы можете использовать его как основу для создания своих расширенных наследников TWebBrowser.</p>
<p>Рассмотрим наиболее интересные с точки зрения программиста методы интерфейса IDocHostUIHandler.</p>
<p>function ShowContextMenu(const dwID: DWORD; const ppt: PPOINT;</p>
<p>  const pcmdtReserved: IUnknown; </p>
<p>  const pdispReserved: IDispatch): HRESULT;</p>
<p>Эта функция вызывается, когда TWebBrowser должен показать контекстное меню. Если Вы отображаете собственное меню, либо хотите подавить меню &#8211; функция должна вернуть S_OK, если меню должен показать TWebBrowser &#8211; S_FALSE.</p>
<p>В неё передаются следующие параметры:</p>
<p>DwID &#8211; идентификатор меню, который может принимать одно из следующих значений:</p>
<p>const</p>
<p>  CONTEXT_MENU_DEFAULT = 0;</p>
<p>  CONTEXT_MENU_IMAGE = 1;</p>
<p>  CONTEXT_MENU_CONTROL = 2;</p>
<p>  CONTEXT_MENU_TABLE = 3;</p>
<p>  CONTEXT_MENU_DEBUG = 4;</p>
<p>  CONTEXT_MENU_1DSELECT = 5;</p>
<p>  CONTEXT_MENU_ANCHOR = 6;</p>
<p>  CONTEXT_MENU_IMGDYNSRC = 7;</p>
<p>в зависимости от значения идентификатора Вы можете вывести подходящее меню </p>
<p>ppt &#8211; координаты, в которых должно быть показано меню</p>
<p>pcmdtReserved &#8211; интерфейс IOleCommandTarget, позволяющий запросить состояние команд и их выполнение</p>
<p>pdispReserved &#8211; интерфейс IDispatch объекта, для которого вызывается меню</p>
<p>Простейшая реализация&nbsp;&nbsp;&nbsp; этого метода может выглядеть следующим образом:</p>
<pre name="code" class="delphi">
function TcustomizedWebBrowser.ShowContextMenu(const dwID: DWORD;
  const ppt: PPOINT;  const pcmdtReserved: IUnknown; 
      const pdispReserved: IDispatch): HRESULT;
begin
  // Предполагаем, что поле FPopupMenu хранит ссылку
  // на компонент TPopupMenu
  if Assigned(FPopupMenu) then begin
    pmContext.Popup(ppt.X, ppt.Y);
    Result := S_OK;
  end 
  else Result := S_FALSE;
end;
</pre>

<p>Для полного запрета контекстного меню метод должен всегда возвращать S_OK.</p>
<p>function GetHostInfo(var pInfo: TDocHostInfo): HRESULT; stdcall;</p>
<p>Приложение может заполнить структуру pInfo, определенную как:</p>
<pre name="code" class="delphi">
TDocHostInfo = packed record
  cbSize: ULONG;
  dwFlags: DWORD;
  dwDoubleClick: DWORD;
end;
</pre>

<p>dwFlags &#8211; битовая маска из следующих флагов:</p>
<p>DOCHOSTUIFLAG_DIALOG &nbsp; &nbsp; &nbsp; &nbsp;</p>
<p>Запрещает выделение текста в форме &nbsp; &nbsp; &nbsp; &nbsp;</p>
<p>DOCHOSTUIFLAG_DISABLE_HELP_MENU &nbsp; &nbsp; &nbsp; &nbsp;</p>
<p>Запрещает контекстное меню &nbsp; &nbsp; &nbsp; &nbsp;</p>
<p>DOCHOSTUIFLAG_NO3DBORDER &nbsp; &nbsp; &nbsp; &nbsp;</p>
<p>Подавляет вывод 3-мерной рамки вокруг компонента &nbsp; &nbsp; &nbsp; &nbsp;</p>
<p>DOCHOSTUIFLAG_SCROLL_NO &nbsp; &nbsp; &nbsp; &nbsp;</p>
<p>Отключает полосы прокрутки &nbsp; &nbsp; &nbsp; &nbsp;</p>
<p>DOCHOSTUIFLAG_DISABLE_SCRIPT_INACTIVE &nbsp; &nbsp; &nbsp; &nbsp;</p>
<p>Запрещает исполнение скриптов  &nbsp; &nbsp; &nbsp; &nbsp;</p>
<p>DOCHOSTUIFLAG_OPENNEWWIN &nbsp; &nbsp; &nbsp; &nbsp;</p>
<p>Открывает ссылки в новых окнах &nbsp; &nbsp; &nbsp; &nbsp;</p>
<p>DOCHOSTUIFLAG_FLAT_SCROLLBAR &nbsp; &nbsp; &nbsp; &nbsp;</p>
<p>Использует плоский стиль для полос прокрутки &nbsp; &nbsp; &nbsp; &nbsp;</p>
<p>DOCHOSTUIFLAG_DIV_BLOCKDEFAULT &nbsp; &nbsp; &nbsp; &nbsp;</p>
<p>При вводе возврата каретки в режиме редактирования будет использоваться тег &lt;DIV &gt;, вместо &lt;P&gt; &nbsp; &nbsp; &nbsp; &nbsp;</p>
<p>DOCHOSTUIFLAG_ACTIVATE_CLIENTHIT_ONLY &nbsp; &nbsp; &nbsp; &nbsp;</p>
<p>Компонент получает фокус только при щелчке мышью в клиентской области окна. При щелчке в не клиентской области (например на полосе прокрутке) компонент фокуса не получает. &nbsp; &nbsp; &nbsp; &nbsp;</p>
<p>dwDoubleClick задает реакцию на двойной щелчок мышью и может принимать одно из следующих значений:</p>
<p>DOCHOSTUIDBLCLK_DEFAULT &nbsp; &nbsp; &nbsp; &nbsp;</p>
<p>Выполнять действие «по умолчанию» &nbsp; &nbsp; &nbsp; &nbsp;</p>
<p>DOCHOSTUIDBLCLK_SHOWPROPERTIES &nbsp; &nbsp; &nbsp; &nbsp;</p>
<p>Показывать окно свойств страницы &nbsp; &nbsp; &nbsp; &nbsp;</p>
<p>DOCHOSTUIDBLCLK_SHOWCODE DOCHOSTUIFLAG_DIALOG &nbsp; &nbsp; &nbsp; &nbsp;</p>
<p>Показывать HTML-код страницы &nbsp; &nbsp; &nbsp; &nbsp;</p>
<p>Метод должен вернуть S_OK или код ошибки OLE</p>
<p>Например, чтобы создать окно с плоскими полосами прокрутки и без 3-мерной рамки необходимо реализовать этот метод следующим образом:</p>
<pre name="code" class="delphi">
function TCustomizedWebBrowser.GetHostInfo(
  var pInfo: TDocHostInfo): HRESULT; stdcall; 
begin
  with pInfo do
    dwFlags := dwFlags or DOCHOSTUIFLAG_NO3DBORDER or 
      DOCHOSTUIFLAG_FLAT_SCROLLBAR;
  Result := S_OK;
end;
 
function TranslateAccelerator(const lpMsg: TMsg;
  const pguidCmdGroup: TGUID; nCmdID: DWORD): HRESULT; stdcall;
</pre>

<p>Позволяет перехватить исполнение команд и обработку «горячих» клавиш и заменить её на свою.</p>
<p>function GetOptionKeyPath(var pchKey: PWideChar; </p>
<p>  dwReserved: DWORD): HRESULT; stdcall;</p>
<p>Позволяет задать путь в реестре, который TWebBrowser будет использовать для хранения настроек. Это дает возможность, например, сделать используемый в программе компонент независимым от текущих настроек Internet Explorer`а.</p>
<p>Путь должен находится под ключом HKEY_CURRENT_USER.</p>
<p>Метод должен выделить память под строку функцией CoTackMemAlloc. Даже в случае ошибки параметр pchKey должен быть инициализирован значением NIL или адресом строки. Метод возвращает S_OK в случае успеха или S_FALSE в противном случае.</p>
<p>Типичная реализация этого метода может выглядеть следующим образом:</p>
<pre name="code" class="delphi">
function TCustomizedWebBrowser.GetOptionKeyPath(
  var pchKey: PWideChar;  dwReserved: DWORD): HRESULT;
var
  ResultLen: Integer;
begin
  Result := S_FALSE;
  // В поле TCustomizedWebBrowser.FOptionKeyPath: String 
  // хранится путь к настройкам
  if Length(FOptionKeyPath) &gt; 0 then
  begin
    // Получаем длину строки UNICODE
    ResultLen := MultiByteToWideChar(CP_ACP, 0, 
      PChar(FOptionKeyPath), -1, NIL, 0);
    // Выделяем память под буфер
    pchKey := CoTaskMemAlloc(ResultLen * SizeOf(WideChar));
    // Если выделение успешно – копируем строку в буфер
    if Assigned(pchKey) then 
    begin
      MultiByteToWideChar(CP_ACP, 0, PChar(FOptionKeyPath), -1, 
        pchKey, ResultLen);
      Result := S_OK;
    end;
  end else begin
    // Свойство не задано – инициализируем параметр в NIL
    pchKey := NIL;
  end;
end;
</pre>

<p>Существует ряд настроек, которые, несмотря на наличие обработчика GetOptionKeyPath в любом случае берутся из стандартных параметров Internet Explorer. Наиболее важными из них являются колонтитулы, используемые при печати. В версиях Internet Explorer до 5.5 включительно единственным способом изменить (или подавить) колонтитулы является запись новых значений в ключ реестра:</p>
<p>HKCU\Software\Microsoft\Internet Explorer\PageSetup </p>
<p>перед печатью и восстановление их после печати.</p>
<p>function GetExternal(var ppDispatch: IDispatch): HRESULT; stdcall;</p>
<p>Позволяет вернуть указатель на реализованный в Вашем приложении интерфейс IDispatch, который будет доступен для скриптов в TWebBrowser. Если Вы не реализуете этого интерфейса &#8211; параметр ppDispatch должен быть инициализирован в NIL. Метод возвращает S_OK в случае успеха или код ошибки OLE в случае ошибки.</p>
<p>Методы этого интерфейса доступны из скриптов, выполняющихся в TWebBrowser следующим образом:</p>
<p>window.external.MethodName</p>
<p>Реализовать IDispatch можно, например, при помощи класса TAutoObject.</p>
<p>function TranslateURL(dwTranslate: DWORD; pchURLIn: PWideChar;</p>
<p>  var ppchURLOut: PWideChar): HRESULT; stdcall;</p>
<p>Позволяет изменить URL по которому осуществляется загрузка страницы. </p>
<p>pchURLIn указывает на строку, содержащую исходный URL. Если Ваше приложение осуществляет трансляцию, оно должно выделить память под новое значение используя функцию CoTaskMemAlloc, заполнить буфер новым значением URL и вернуть S_OK.</p>
<p>В противном случае Вы должны присвоить ppchURLOut значение NIL и вернуть S_FALSE. В случае возникновения ошибки метод должен вернуть OLE-код ошибки.</p>
<p>Обработчик вызывается только при интерактивном переходе по ссылке из TWebBrowser и не вызывается при переходе при помощи метода Navigate.</p>
Доступ к документной модели TWebBrowser</p>
<p>В Internet Explorer реализовано расширение HTML, под названием Dynamic HTML (DHTML). Эта модель представляет все элементы HTML-документа в виде набора коллекций объектов, доступных для изменения. Скрипты, встроенные в страницы и приложения, имеющие доступ к этим коллекциям могут находить и изменять их элементы, добавлять новые, причем изменения будут немедленно отражены в окне TWebBrowser. Иерархическое объектное представление HTML объектов называется DOM (Document Object Model) .</p>
<p>DOM в IE ActiveX доступна программисту в виде набора COM интерфейсов. Отправной точкой для доступа к ней служит свойство </p>
<p>property Document: IDispatch;</p>
<p>Это свойство обеспечивает доступ к интерфейсу, IHtmlDocument2 позволяющему работать с содержимым документа. Для получения интерфейса необходимо запросить его при помощи оператора as</p>
<p>var</p>
<p> Document: IHtmlDocument2;</p>
<p>...</p>
<p>Document := WebBrowser.Document as IHtmlDocument2;</p>
<p>Документ в DOM представляет из себя набор коллекций элементов. Для доступа к коллекции служит интерфейс IHtmlElementCollection, а к элементу коллекции &#8211; IHtmlElement. Следующий пример выводит все тэги, имеющиеся в текущем документе и текст внутри тэгов.</p>
<pre name="code" class="delphi">
procedure TForm1.Button1Click(Sender: TObject);
var
  HtmlDocument: IHtmlDocument2;
  HtmlCollection: IHtmlElementCollection;
  HtmlElement: IHtmlElement;
  I: Integer;
begin
  Memo1.Lines.Clear;
  HtmlDocument := WebBrowser.Document as IHtmlDocument2;
  HtmlCollection := HtmlDocument.All;
  for I := 0 to HtmlCollection.Length - 1 do begin
    HtmlElement := HtmlCollection.Item(i, 0) as IHtmlElement;
    Memo1.Lines.Add(HtmlElement.TagName + ' ' +
      HtmlElement.InnerText);
end;
</pre>

<p>Возможно динамическое создание документов в памяти, без необходимости записи их на диск и вызова метода Navigate с протоколом 'file://'</p>
<p>Проиллюстрируем работу с документной модели TWebBrowser на примере. Расположим на форме компоненты TWebBrowser, TMemo и три TButton, и создадим следующие обработчики событий:</p>
<pre name="code" class="delphi">
uses MSHTML, ActiveX;
 
procedure TForm1.FormCreate(Sender: TObject);
begin
  // Инициализируем пустой документ в TWebBrowser
  WebBrowser1.Navigate('about:blank');
end;
 
procedure TForm1.Button1Click(Sender: TObject);
var
  Document: IHTMLDocument2;
  V: OleVariant;
begin
  // Этот метод переписывает в TWebBrowser HTML-
  // документ из TMemo
  Document := WebBrowser1.Document as IHtmlDocument2;
  V := VarArrayCreate([0, 0], varVariant);
  V[0] := Memo1.Text;
  Document.Write(PSafeArray(TVarData(v).VArray));
  Document.Close;
end;
 
procedure TForm1.Button3Click(Sender: TObject);
var
  Document: IHTMLDocument2;
  Collection: IHTMLElementCollection;
  Element: IHTMLElement;
  I: Integer;
begin
  // Этот метод модифицирует текст документа при помощи DHTML
  Document := WebBrowser1.Document as IHtmlDocument2;
  Collection := Document.all;
  Collection := Collection.Tags('BODY') as IHTMLElementCollection;
  Element := Collection.Item(NULL, 0) as IHTMLElement;
  Element.InnerText := 'Modifyed by DHTML';
end;
 
procedure TForm1.Button2Click(Sender: TObject);
var
  Document: IHTMLDocument2;
begin
  // Этот метод позволяет просмотреть в TMemo код HTML
  // документа из TWebBrowser
  Document := WebBrowser1.Document as IHtmlDocument2;
  Memo1.Text := (Document.all.Item(NULL, 0) 
    as IHTMLElement).OuterHTML;
end;
</pre>

<p>В Memo1.Lines в дизайнере запишем следующий текст:</p>
<p>&lt;HTML&gt;</p>
<p>  &lt;HEAD&gt;</p>
<p> &nbsp;&nbsp; &lt;TITLE&gt;Hello World&lt;/TITLE&gt;</p>
<p>  &lt;/HEAD&gt;</p>
<p>  &lt;BODY&gt;</p>
<p>Hello again !</p>
<p>  &lt;/BODY&gt;</p>
<p>&lt;/HTML&gt;</p>
<p>Таким образом, мы получили возможность динамически создавать HTML документы и предоставлять их пользователю.</p>
<p>Тенцер А. Л.</p>
<p>ICQ UIN 15925834</p>
<p>tolik@katren.nsk.ru</p>
</div>
<!-- Actual content end -->
<hr />
<div id="footer">
<p>&copy; DRKB Library, 2010<br />Разработка и поддержка &mdash; <a href="http://www.drkb.ru/" target="_blank">Quadr0</a></p>
</div>
</div>
</body>
</html>
