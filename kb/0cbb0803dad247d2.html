<html>
<head>
  <title>Получение текущего чертежа из AutoCAD в формате DXF</title>
  <meta http-equiv="Content-Type" content="text/html; charset=windows-1251" />
  <link type="text/css" href="css/css.css" rel="stylesheet" />
  <link type="text/css" href="css/sh.css" rel="stylesheet" />
  <script language="javascript" src="js/shInit.js"></script>
  <script language="javascript" src="js/shCore.js"></script>
  <script language="javascript" src="js/shBrushDelphi.js"></script>
  <script language="javascript" src="js/shBrushSql.js"></script>
</head>
<body>
<div id="layout">
<div id="header">
  <div id="logo"><img id="logo" src="img/logo.png" /></div>
  <div id="navigation">
    <p id="navigation">
      <a class="navigation" onclick="prev();" href="#">Предыдущая</a><br />
      <a class="navigation" onclick="up();" href="#">Наверх</a><br />
      <a class="navigation" onclick="next();" href="#">Следующая</a>
    </p>
  </div>
</div>
<div id="content">
<div id="explorer">DRKB Explorer</div>
  <h1 id="title">Получение текущего чертежа из AutoCAD в формате DXF</h1>
<div id="date">01.01.2010</div>
<!-- Actual content start -->

<pre name="code" class="delphi">
{ **** UBPFD *********** by kladovka.net.ru ****
&gt;&gt; Получение текущего чертежа из AutoCAD в формате DXF
 
Функция импортирует активного чертёж из AutoCAD в формате DXF и записывает полученные данные в список List. В случае успешного завершения функция возвращает True. В случае ошибки (AutoCAD не загружен, cбой OLE и проч.) функция возвращает False не внося изменений в список строк
 
Зависимости: Windows, SysUtils, ComObj, ActiveX
Автор:       Dimka Maslov, mainbox@endimus.ru, ICQ:148442121, Санкт-Петербург
Copyright:   Dimka Maslov
Дата:        22 ноября 2002 г.
********************************************** }
 
function GetAcadDXFText(List: TStrings): Boolean;
var
 TempDir, FileName: string;
 ClassID: TGUID;
 Unknown: IUnknown;
 Dispatch: IDispatch;
 App, Doc, Sel: Variant;
 TempList: TStringList;
const
 Ext = 'dxf';
 DotExt = '.'+Ext;
begin
 SetLength(TempDir, MAX_PATH);
 GetTempPath(MAX_PATH, PChar(TempDir));
 SetLength(TempDir, StrLen(@TempDir[1]));
 TempDir:=IncludeTrailingBackslash(TempDir);
 repeat
  FileName:=TempDir+IntToHex(LoWord(GetTickCount), 4)+DotExt;
 until not FileExists(FileName);
 SetLength(FileName, Length(FileName)-Length(DotExt));
 Result:=True;
 try
  ClassID := ProgIDToClassID('AutoCAD.Application');
  if not Succeeded(GetActiveObject(ClassID, nil, Unknown)) then Abort;
  Unknown.QueryInterface(IDispatch, Dispatch);
  App:=Dispatch;
  try
   Doc:=App.ActiveDocument;
   try
    Sel:=Doc.SelectionSets.Add('TEMP');
    try
     Doc.Export(FileName, Ext, Sel);
    finally
     Sel:=Unassigned;
    end;
   finally
    Doc:=Unassigned;
   end;
  finally
   App:=Unassigned;
  end;
  FileName:=FileName+DotExt;
  TempList:=TStringList.Create;
  try
   TempList.LoadFromFile(FileName);
   List.Assign(TempList);
  finally
   TempList.Free;
  end;
 except
  Result:=False;
 end;
 if FileExists(FileName) then DeleteFile(FileName);
end; 
</pre>

<p> Пример использования:</p>
<pre name="code" class="delphi">
if not GetAcadDXFText(Memo1.Lines) then
 ShowMessage('Невозможно получить данные от AutoCAD'); 
</pre>

</div>
<!-- Actual content end -->
<hr />
<div id="footer">
<p>&copy; DRKB Library, 2010<br />Разработка и поддержка &mdash; <a href="http://www.drkb.ru/" target="_blank">Quadr0</a></p>
</div>
</div>
</body>
</html>
