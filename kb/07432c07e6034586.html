<html>
<head>
  <title>Интерфейсы и TComponent</title>
  <meta http-equiv="Content-Type" content="text/html; charset=windows-1251" />
  <link type="text/css" href="css/css.css" rel="stylesheet" />
  <link type="text/css" href="css/sh.css" rel="stylesheet" />
  <script language="javascript" src="js/shInit.js"></script>
  <script language="javascript" src="js/shCore.js"></script>
  <script language="javascript" src="js/shBrushDelphi.js"></script>
  <script language="javascript" src="js/shBrushSql.js"></script>
</head>
<body>
<div id="layout">
<div id="header">
  <div id="logo"><img id="logo" src="img/logo.png" /></div>
  <div id="navigation">
    <p id="navigation">
      <a class="navigation" onclick="prev();" href="#">Предыдущая</a><br />
      <a class="navigation" onclick="up();" href="#">Наверх</a><br />
      <a class="navigation" onclick="next();" href="#">Следующая</a>
    </p>
  </div>
</div>
<div id="content">
<div id="explorer">DRKB Explorer</div>
  <h1 id="title">Интерфейсы и TComponent</h1>
<div id="date">01.01.2010</div>
<!-- Actual content start -->

<p>В базовом классе VCL TComponent имеется полный набор методов, позволяющих реализовать интерфейс IUnknown, хотя сам класс не реализует этого интерфейса. Это позволяет наследникам TComponent реализовывать интерфейсы не заботясь о реализации IUnknown. Однако, методы TComponent._AddRef и TComponent._Release на этапе выполнения программы не реализуют механизма подсчета ссылок, и, соответственно, для классов-наследников TComponent, реализующих интерфейсы не действует автоматическое управление памятью. Это позволяет запрашивать у них интерфейсы не опасаясь, что объект будет удален из памяти при выходе переменной за область видимости. Таким образом, следующий код совершенно корректен и безопасен:</p>
<pre name="code" class="delphi">
type
  IGetData = interface
  ['{B5266AE0-5E77-11D4-84DD-9153115ABFC3}']
    function GetData: String;
  end;
 
  TForm1 = class(TForm, IGetData)
  private
    function GetData: String;
  end;
…
var
  I: Integer;
  GD: IGetData;
  S: String;
begin
  S := '';
  for I := 0 to Pred(Screen.FormCount) do begin
    if Screen.Forms[I].GetInterface(IGetData, GD) then
      S := S + GD.GetData + #13;
  end;
  ShowMessage(S);
end;
</pre>


<p>Этот код проверяет наличие у всех форм в приложении реализации интерфейса IGetData и, если форма реализует этот интерфейс &#8211; вызывает его метод.</p>

<p>Использование интерфейсов внутри программы.</p>
<p>Рассмотренное выше поведение TComponent позволяет строить просто и не теряя строгой типизации связывать компоненты приложения и единообразно вызывать их методы не требуя, чтобы компоненты были унаследованы от общего предка. Достаточно лишь реализовать в компоненте интерфейс, а в вызывающей программе &#8211; проверить его наличие.</p>
<p>Рассмотрим, в качестве примера, MDI приложение, имеющее много различных форм и единую панель инструментов. Предположим, что на этой панели инструментов имеются команды «Сохранить», «Загрузить» и «Очистить», однако, каждое из окон реагирует на эти команды по-разному.</p>
<p>Создадим модуль с объявлениями интерфейсов</p>
<pre name="code" class="delphi">
unit ToolbarInterface;
 
interface
 
type
  TCommandType = (ctSave, ctLoad, ctClear);
  TCommandTypes = set of TCommandType;
  TSaveType = (stSave, stSaveAS);
 
  IToolBarCommands = interface
  ['{B5266AE1-5E77-11D4-84DD-9153115ABFC3}']
    function SupportedCommands: TCommandTypes;
    function Save(AType: TSaveType): Boolean;
    procedure Load;
    procedure Clear;
  end;
 
implementation
 
end.
</pre>


<p>Интерфейс IToolBarCommands описывает набор методов, которые должны реализовать поддерживающие работу с панелью кнопок формы. Метод SupportedCommands возвращает список поддерживаемых формой команд</p>
<p>Создадим три дочерние формы Form2, Form3 и Form4, установим им свойство FormStyle = fsMDIChild. </p>
<p>Form2 умеет выполнять все три команды</p>
<pre name="code" class="delphi">
type
  TForm2 = class(TForm, IToolBarCommands)
  private
    function SupportedCommands: TCommandTypes;
    function Save(AType: TSaveType): Boolean;
    procedure Load;
    procedure Clear;
  end;
 
{ TForm2 }
procedure TForm2.Clear;
begin
  ShowMessage('TForm2.Clear');
end;
 
procedure TForm2.Load;
begin
  ShowMessage('TForm2.Load');
end;
 
function TForm2.Save(AType: TSaveType): Boolean;
begin
  ShowMessage('TForm2.Save');
  Result := TRUE;
end;
 
function TForm2.SupportedCommands: TCommandTypes;
begin
  Result := [ctSave, ctLoad, ctClear]
end;
</pre>


<p>Form3 умеет выполнять только команду Clear</p>
<pre name="code" class="delphi">
type
  TForm3 = class(TForm, IToolBarCommands)
  private
    function SupportedCommands: TCommandTypes;
    function Save(AType: TSaveType): Boolean;
    procedure Load;
    procedure Clear;
  end;
 
{ TForm3 }
procedure TForm3.Clear;
begin
  ShowMessage('TForm3.Clear');
end;
 
procedure TForm3.Load;
begin
  // Метод ничего не делает, но должен присутствовать
  // для корректной реализации интерфейса
end;
 
function TForm3.Save(AType: TSaveType): Boolean;
begin
end;
 
function TForm3.SupportedCommands: TCommandTypes;
begin
  Result := [ctClear]
end;
</pre>


<p>И, наконец, Form4 вообще не реализует интерфейса IToolBarCommands и не умеет откликаться ни на одну команду</p>
<p>На главной форме приложения поместим ActionList и создадим три компонента TAction. Также разместим на ней TToolBar и назначим её кнопкам соответствующие TAction. </p>
<pre name="code" class="delphi">
type
  TForm1 = class(TForm)
    ToolBar1: TToolBar;
    ImageList1: TImageList;
    ActionList1: TActionList;
    acLoad: TAction;
    acSave: TAction;
    acClear: TAction;
    tbSave: TToolButton;
    tbLoad: TToolButton;
    tbClear: TToolButton;
    procedure acLoadExecute(Sender: TObject);
    procedure ActionList1Update(Action: TBasicAction;
      var Handled: Boolean);
    procedure acSaveExecute(Sender: TObject);
    procedure acClearExecute(Sender: TObject);
  end;
</pre>


<p>Наиболее интересен метод ActionList1Update, в котором проверяются поддерживаемые активной формой команды и настраивается интерфейс главной формы. Если нет активной дочерней формы, либо она не поддерживает интерфейс IToolBarCommands, все команды запрещаются, в противном случае &#8211; разрешаются только поддерживаемые формой команды.</p>
<pre name="code" class="delphi">
procedure TForm1.ActionList1Update(Action: TBasicAction;
  var Handled: Boolean);
var
  Supported: TCommandTypes;
  TC: IToolBarCommands;
begin
  if Assigned(ActiveMDIChild)
     and ActiveMDIChild.GetInterface(IToolBarCommands, TC) then
    Supported := TC.SupportedCommands
  else
    Supported := [];
  acSave.Enabled := ctSave in Supported;
  acLoad.Enabled := ctLoad in Supported;
  acClear.Enabled := ctClear in Supported;
end;
</pre>


<p>При активизации команд проверяется наличие активной дочерней формы, у неё запрашивается интерфейс IToolBarCommands и вызывается соответствующий его метод</p>
<pre name="code" class="delphi">
procedure TForm1.acLoadExecute(Sender: TObject);
var
  TC: IToolBarCommands;
begin
  if Assigned(ActiveMDIChild)
     and ActiveMDIChild.GetInterface(IToolBarCommands, TC) then
    TC.Load;
end;
 
procedure TForm1.acSaveExecute(Sender: TObject);
var
  TC: IToolBarCommands;
begin
  if Assigned(ActiveMDIChild)
     and ActiveMDIChild.GetInterface(IToolBarCommands, TC) then
    if not TC.Save(stSaveAS) then
      ShowMessage('Not Saved !!!');
end;
 
procedure TForm1.acClearExecute(Sender: TObject);
var
  TC: IToolBarCommands;
begin
  if Assigned(ActiveMDIChild)
     and ActiveMDIChild.GetInterface(IToolBarCommands, TC) then
    TC.Clear;
end;
</pre>


<p>Работа программы представлена на рисунке.</p>
<img src="pic/clip0058.png" width="427" height="362" border="0" alt="clip0058"></p>
<p>Того же эффекта можно добиться и другими методами (например, унаследовав все дочерние формы от единого предка, либо обмениваясь с ними сообщениями), однако эти методы имеют ряд существенных недостатков.</p>
<p>Так, при обмене сообщениями мы теряем строгую типизацию и вынуждены передавать параметры через целые числа, а при визуальном наследовании мы привязываем себя к родительскому классу, что не всегда удобно. К тому же можно определить множество интерфейсов и реализовывать в каждой из дочерних форм лишь необходимые, а в случае с наследованием все формы должны будут реализовывать все общие методы.</p>
<p>Использование интерфейсов для реализации Plug-In</p>
<p>Еще более удобно использовать интерфейсы для реализации модулей расширения программы (Plug-In). Как правило такой модуль экспортирует ряд известных главной программе методов, которые могут быть вызваны из него. В то же время, часто ему нужно обращаться к каким-либо функциям вызывающей программы. И то и другое удобно реализуется при помощи интерфейсов.</p>
<p>В качестве примера, реализуем несложную программу, использующую Plug-In для загрузки данных.</p>
<p>Объявим интерфейсы модуля расширения и внутреннего API программы.</p>
<pre name="code" class="delphi">
unit PluginInterface;
 
interface
 
type
  IAPI = interface
  ['{64CFF1E0-61A3-11D4-84DD-B18D6F94141F}']
    procedure ShowMessage(const S: String);
  end;
 
  ILoadFilter = interface
  ['{64CFF1E1-61A3-11D4-84DD-B18D6F94141F}']
    procedure Init(const FileName: String; API: IAPI);
    function GetNextLine(var S: String): Boolean;
  end;
 
implementation
 
end.
</pre>


<p>Этот модуль должен использоваться как в Plug-In, так и в основной программе и гарантирует использование ими идентичных интерфейсов.</p>
<p>Plug-In представляет собой DLL, экспортирующую функцию CreateFilter, возвращающую ссылку на интерфейс ILoadFilter. Главный модуль сначала должен вызвать метод Init, передав в него имя файла и ссылку на интерфейс внутреннего API, а затем вызывает метод GetNextLine до тех пор, пока он не вернет FALSE.</p>
<p>Рассмотрим код модуля расширения:</p>
<pre name="code" class="delphi">
library ImpTxt;
 
uses
  ShareMem, SysUtils, Classes, PluginInterface;
 
{$R *.RES}
 
type
  TTextFilter = class(TInterfacedObject, ILoadFilter)
  private
    FAPI: IAPI;
    F: TextFile;
    Lines: Integer;
    InitSuccess: Boolean;
    procedure Init(const FileName: String; API: IAPI);
    function GetNextLine(var S: String): Boolean;
  public
    destructor Destroy; override;
  end;
{ TTextFilter }
 
procedure TTextFilter.Init(const FileName: String; API: IAPI);
begin
  FAPI := API;
  {$I-}
  AssignFile(F, FileName);
  Reset(F);
  {$I+}
  InitSuccess := IOResult = 0;
  if not InitSuccess then
    API.ShowMessage('Ошибка инициализации загрузки');
end;
</pre>


<p>Метод Init выполняет две задачи: сохраняет ссылку на интерфейс API главного модуля для дальнейшего использования и пытается открыть файл с данными. Если файл открыт успешно &#8211; выставляется внутренний флаг InitSuccess</p>
<pre name="code" class="delphi">
function TTextFilter.GetNextLine(var S: String): Boolean;
begin
  if InitSuccess then begin
    Inc(Lines);
    Result := not Eof(F);
    if Result then begin
      Readln(F, S);
      FAPI.ShowMessage('Загружено ' + IntToStr(Lines) + ' строк.');
    end;  
  end else
    Result := FALSE;
end;
</pre>


<p>Метод GetNextLine считывает следующую строку данных и возвращает TRUE, если это удалось, либо FALSE, в случае окончания файла. Кроме этого, он при помощи API, предоставляемого главным модулем информирует пользователя о ходе загрузки.</p>
<pre name="code" class="delphi">
destructor TTextFilter.Destroy;
begin
  FAPI := NIL;
  if InitSuccess then
    CloseFile(F);
  inherited;
end;
</pre>


<p>В деструкторе мы обнуляем ссылку на API главного модуля, уничтожая его .и закрываем файл.</p>
<pre name="code" class="delphi">
function CreateFilter: ILoadFilter;
begin
  Result := TTextFilter.Create;
end;
</pre>


<p>Эта функция создает экземпляр класса, реализующего интерфейс ILoadFilter. Ссылок на экземпляр сохранять не нужно, он будет освобожден автоматически.</p>
<pre name="code" class="delphi">
exports
  CreateFilter;  // Функция должна быть экспортирована из DLL
 
begin
end.
</pre>


<p>Теперь полученную DLL можно использовать из основной программы.</p>
<pre name="code" class="delphi">
type
  TAPI = class(TInterfacedObject, IAPI)
    procedure ShowMessage(const S: String);
  end;
 
 
{ TAPI }
procedure TAPI.ShowMessage(const S: String);
begin
  with (Application.MainForm as TForm1).StatusBar1 do begin
    SimpleText := S;
    Update;
  end;
end;
</pre>


<p>Класс TAPI реализует API, предоставляемый модулю расширения. Функция ShowMessage выводит сообщения модуля в Status Bar главной формы приложения.</p>
<pre name="code" class="delphi">
type
  TCreateFilter = function: ILoadFilter;
 
procedure TForm1.LoadData(FileName: String);
var
  PluginName: String;
  Ext: String;
  hPlugIn: THandle;
  CreateFilter: TCreateFilter;
  Filter: ILoadFilter;
  S: String;
begin
</pre>


<p>Подготавливаем TMemo к загрузке данных</p>
<pre name="code" class="delphi">
  Memo1.Lines.Clear;
  Memo1.Lines.BeginUpdate;
</pre>

<p>Получаем имя модуля с фильтром для выбранного расширения файла. Описания модулей хранятся в файле plugins.ini в секции [Filters] в виде строк формата:</p>
<p> &lt;расширение&gt; = &lt;имя модуля&gt;, например:</p>

<p>[Filters]</p>
<p>TXT=IpTXT.DLL</p>
<pre name="code" class="delphi">
  try
    Ext := ExtractFileExt(FileName);
    Delete(Ext, 1, 1);
    with TIniFile.Create(ExtractFilePath(ParamStr(0)) + 'plugins.ini') do
    try
      PlugInName := ReadString('Filters', Ext, '');
    finally
      Free;
    end;
</pre>


<p>Далее, пытаемся загрузить модуль и найти в нем функцию CreateFilter</p>
<pre name="code" class="delphi">
 
    hPlugIn := LoadLibrary(PChar(PluginName));
    try
      CreateFilter := GetProcAddress(hPlugIn, 'CreateFilter');
      if Assigned(CreateFilter) then begin
</pre>

<p>Функция найдена, создает экземпляр фильтра и инициализируем его. Поскольку внутренний API реализован тоже как интерфейс &#8211; нет необходимости сохранять ссылку на него.</p>

<pre name="code" class="delphi">
 Filter := CreateFilter;
        try
          Filter.Init(FileName, TAPI.Create);
</pre>


<p>Загружаем данные при помощи созданного фильтра</p>
<pre name="code" class="delphi">
          while Filter.GetNextLine(S) do
            Memo1.Lines.Add(S);
</pre>


<p>Перед выгрузкой DLL из памяти необходимо обязательно освободить ссылку на интерфейс Plug-In, иначе это произойдет по выходу из функции и вызовет Access Violation</p>
<pre name="code" class="delphi">
        finally
          Filter := NIL;
        end;
      end else raise Exception.Create('Не могу загрузить фильтр');
</pre>


<p>Выгружаем DLL и обновляем TMemo</p>
<pre name="code" class="delphi">
    finally
      FreeLibrary(hPlugIn);
    end;
  finally
    Memo1.Lines.EndUpdate;
  end;
end;
</pre>


<p>Таким образом, несложно реализовать модули расширения для загрузки данных из форматов, отличающихся от текстового и единообразно работать с ними.</p>
<p>Преимущества данного способа становятся особенно очевидными при реализации сложных модулей расширения, интерфейс с которыми состоит из многих методов.</p>
<p>!  &nbsp; &nbsp; &nbsp; &nbsp;Поскольку в EXE и DLL используются длинные строки, не забудьте включить в список uses обоих проектов модуль ShareMem. Другим вариантом решения проблемы передачи строк является использование типа данных WideString. Для них распределением памяти занимается OLE, причем делает это независимо от модуля, из которого была создана строка.</p>

</div>
<!-- Actual content end -->
<hr />
<div id="footer">
<p>&copy; DRKB Library, 2010<br />Разработка и поддержка &mdash; <a href="http://www.drkb.ru/" target="_blank">Quadr0</a></p>
</div>
</div>
</body>
</html>
