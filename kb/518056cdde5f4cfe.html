<html>
<head>
  <title>Как получить список инсталлированных програм?</title>
  <meta http-equiv="Content-Type" content="text/html; charset=windows-1251" />
  <link type="text/css" href="css/css.css" rel="stylesheet" />
  <link type="text/css" href="css/sh.css" rel="stylesheet" />
  <script language="javascript" src="js/shInit.js"></script>
  <script language="javascript" src="js/shCore.js"></script>
  <script language="javascript" src="js/shBrushDelphi.js"></script>
  <script language="javascript" src="js/shBrushSql.js"></script>
</head>
<body>
<div id="layout">
<div id="header">
  <div id="logo"><img id="logo" src="img/logo.png" /></div>
  <div id="navigation">
    <p id="navigation">
      <a class="navigation" onclick="prev();" href="#">Предыдущая</a><br />
      <a class="navigation" onclick="up();" href="#">Наверх</a><br />
      <a class="navigation" onclick="next();" href="#">Следующая</a>
    </p>
  </div>
</div>
<div id="content">
<div id="explorer">DRKB Explorer</div>
  <h1 id="title">Как получить список инсталлированных програм?</h1>
<div id="date">01.01.2010</div>
<!-- Actual content start -->

<pre name="code" class="delphi">
uses 
  Registry; 
 
procedure TForm1.Button1Click(Sender: TObject); 
const 
  UNINST_PATH = 'SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall'; 
var 
  Reg: TRegistry; 
  SubKeys: TStringList; 
  ListItem: TlistItem; 
  i: integer; 
  sDisplayName, sUninstallString: string; 
begin 
{ 
  ListView1.ViewStyle := vsReport; 
  ListView1.Columns.add; 
  ListView1.Columns.add; 
  ListView1.Columns[0].caption := 'DisplayName'; 
  ListView1.Columns[1].caption := 'UninstallString'; 
  ListView1.Columns[0].Width := 300; 
  ListView1.Columns[1].Width := 300; 
} 
  Reg := TRegistry.Create; 
  with Reg do 
    try 
      with ListView1.Items do 
        try 
          BeginUpdate; 
          Clear; 
          RootKey := HKEY_LOCAL_MACHINE; 
          if OpenKeyReadOnly(UNINST_PATH) then 
          begin 
            SubKeys := TStringList.Create; 
            try 
              GetKeyNames(SubKeys); 
              CloseKey; 
              for i := 0 to subKeys.Count - 1 do 
                if OpenKeyReadOnly(Format('%s\%s', [UNINST_PATH, SubKeys[i]])) then 
                  try 
                    sDisplayName     := ReadString('DisplayName'); 
                    sUninstallString := ReadString('UninstallString'); 
                    if sDisplayName &lt;&gt; '' then 
                    begin 
                      ListItem         := Add; 
                      ListItem.Caption := sDisplayName; 
                      ListItem.subitems.Add(sUninstallString); 
                    end; 
                  finally 
                    CloseKey; 
                  end; 
            finally 
              SubKeys.Free; 
            end; 
          end; 
        finally 
          ListView1.AlphaSort; 
          EndUpdate; 
        end; 
    finally 
      CloseKey; 
      Free; 
    end; 
end; 
</pre>

<p>Взято с сайта <a href="http://www.swissdelphicenter.ch/en/tipsindex.php" target="_blank">http://www.swissdelphicenter.ch/en/tipsindex.php</a></p>
</div>
<!-- Actual content end -->
<hr />
<div id="footer">
<p>&copy; DRKB Library, 2010<br />Разработка и поддержка &mdash; <a href="http://www.drkb.ru/" target="_blank">Quadr0</a></p>
</div>
</div>
</body>
</html>
