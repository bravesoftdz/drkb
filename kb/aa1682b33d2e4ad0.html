<html>
<head>
  <title>Проверка наличия .NET Framework и определение его версии</title>
  <meta http-equiv="Content-Type" content="text/html; charset=windows-1251" />
  <link type="text/css" href="css/css.css" rel="stylesheet" />
  <link type="text/css" href="css/sh.css" rel="stylesheet" />
  <script language="javascript" src="js/shInit.js"></script>
  <script language="javascript" src="js/shCore.js"></script>
  <script language="javascript" src="js/shBrushDelphi.js"></script>
  <script language="javascript" src="js/shBrushSql.js"></script>
</head>
<body>
<div id="layout">
<div id="header">
  <div id="logo"><img id="logo" src="img/logo.png" /></div>
  <div id="navigation">
    <p id="navigation">
      <a class="navigation" onclick="prev();" href="#">Предыдущая</a><br />
      <a class="navigation" onclick="up();" href="#">Наверх</a><br />
      <a class="navigation" onclick="next();" href="#">Следующая</a>
    </p>
  </div>
</div>
<div id="content">
<div id="explorer">DRKB Explorer</div>
  <h1 id="title">Проверка наличия .NET Framework и определение его версии</h1>
<div id="date">01.01.2010</div>
<!-- Actual content start -->

<pre name="code" class="delphi">
{ **** UBPFD *********** by kladovka.net.ru ****
&gt;&gt; Проверка наличия .NET Framework и определение его версии
 
Функция возвращает номер последней установленной версии .NET Framework на компьютере, или пустую строку, если платформа .NET не установлена, либо установлена криво.
 
Зависимости: Registry, SysUtils
Автор:       Dimka Maslov, mainbox@endimus.com, ICQ:148442121, Санкт-Петербург
Copyright:   Dimka Maslov
Дата:        19 января 2004 г.
********************************************** }
 
function DotNetVersion: String;
var
 R: TRegistry;
 L: TStringList;
 S: string;
 i, MaxIndex, N, Code: Integer;
 V, MaxVersion: Double;
const
 RegKey = 'Software\Microsoft\.NETFramework\Policy';
begin
 Result := '';
 R := TRegistry.Create;
 try
  R.RootKey := HKEY_LOCAL_MACHINE;
  if R.KeyExists(RegKey) then begin
   R.OpenKeyReadOnly(RegKey);
   L := TStringList.Create;
   try
    R.GetKeyNames(L);
    MaxVersion := -1.0;
    MaxIndex := -1;
    for i := 0 to L.Count - 1 do begin
     S := L[i];
     if UpCase(S[1]) = 'V' then begin
      Delete(S, 1, 1);
      Val(S, V, Code);
      if (Code = 0) and (V &gt; MaxVersion) then begin
       MaxVersion := V;
       MaxIndex := i;
      end;
     end;
    end;
    if MaxIndex &lt;&gt; - 1 then begin
     S := L[MaxIndex];
     R.CloseKey;
     R.OpenKeyReadOnly(RegKey+'\'+S);
     R.GetValueNames(L);
     MaxIndex := -1;
     for i := 0 to L.Count - 1 do begin
      Val(L[i], N, Code);
      if (Code = 0) and (N &gt; MaxIndex) then MaxIndex := N;
     end;
     Result := S;
     Delete(Result, 1, 1);
     if MaxIndex &lt;&gt; -1 then Result := Result + '.' + IntToStr(MaxIndex);
    end;
   finally
    L.Free;
   end;
  end;
 finally
  R.Free;
 end;
end;
</pre>

</div>
<!-- Actual content end -->
<hr />
<div id="footer">
<p>&copy; DRKB Library, 2010<br />Разработка и поддержка &mdash; <a href="http://www.drkb.ru/" target="_blank">Quadr0</a></p>
</div>
</div>
</body>
</html>
