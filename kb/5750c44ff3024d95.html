<html>
<head>
  <title>Delphi и 1C &ndash; экспорт и импорт</title>
  <meta http-equiv="Content-Type" content="text/html; charset=windows-1251" />
  <link type="text/css" href="css/css.css" rel="stylesheet" />
  <link type="text/css" href="css/sh.css" rel="stylesheet" />
  <script language="javascript" src="js/shInit.js"></script>
  <script language="javascript" src="js/shCore.js"></script>
  <script language="javascript" src="js/shBrushDelphi.js"></script>
  <script language="javascript" src="js/shBrushSql.js"></script>
</head>
<body>
<div id="layout">
<div id="header">
  <div id="logo"><img id="logo" src="img/logo.png" /></div>
  <div id="navigation">
    <p id="navigation">
      <a class="navigation" onclick="prev();" href="#">Предыдущая</a><br />
      <a class="navigation" onclick="up();" href="#">Наверх</a><br />
      <a class="navigation" onclick="next();" href="#">Следующая</a>
    </p>
  </div>
</div>
<div id="content">
<div id="explorer">DRKB Explorer</div>
  <h1 id="title">Delphi и 1C &ndash; экспорт и импорт</h1>
<div id="date">01.01.2010</div>
<!-- Actual content start -->

<p id="author">Автор: Александр Авдошин</p>
<p>Специально для Королевства Delphi </p>

<p>Довольно часто перед программистами, работающими в небольших компаниях, стоит проблема импорта данных из программы "1С:Предприятие", или экспорта в нее же. Причин тому может быть множество - например, желание автоматизировать обновление прайс-листа на веб-страничке компании на основании реальных данных, или же автоматизация ввода первичных документов, отправляемых по электронной почте компанией-поставщиком. Какая бы задача подобного рода ни стояла перед программистом, она, как правило, успешно решается с помощью связки Delphi-1C. В этой статье я хотел бы дать рекомендации и разъяснить некоторые аспекты использования механизма OLE Automation применительно к программе "1С:Предприятие версия 7.7". </p>

<p>Перед прочтением статьи я настоятельно рекомендую Вам ознакомиться с книгой "Delphi 4 Unleashed" Чарльза Калверта и с главой "Связь с внешними приложениями посредством механизмов DDE и OLE Automation" книги "1С:Предприятие 7.7 Описание встроенного языка". Также я предполагаю, что вы имеете опыт программирования как в среде Delphi, так и в среде "1С:Предприятие". </p>

<p>Первые шаги </p>

<p>Ну, во-первых, прежде чем использовать все возможности программы "1С:Предприятие", необходимо сначала создать соответствующий OLE-объект. Идентификатор этого OLE-объекта зависит от версии и типа установленной программы "1С:Предприятие": </p>

<p>V1CEnterprise.Application - версия независимый ключ </p>
<p>V77.Application - версия зависимый ключ </p>
<p>V77S.Application - версия зависимый ключ, SQL-версия </p>
<p>V77L.Application - версия зависимый ключ, локальная версия </p>
<p>V77M.Application - версия зависимый ключ, сетевая версия </p>
<p>Например, создадим OLE-объект для сервера "1С:Предприятие". Для простоты создадим объект без привязки к конкретной версии и типу программы: </p>
<pre name="code" class="delphi">
procedure TForm1.Create1C;
var
  onesobj: Olevariant;
begin
  onesobj := createoleobject('V1CEnterprise.Application');
end;
 
 
</pre>



<p>Затем мы должны проинициализировать систему методом Initialize, имеющим следующие параметры: </p>

<p>Initialize(&lt;Имя_Объекта&gt;.RMTrade,&lt;КоманднаяСтрока&gt;,&lt;ПустаяСтрока&gt;), где:</p>
<p>&lt;Имя_Объекта&gt; - Идентификатор созданного OLE объекта</p>
<p>&lt;КоманднаяСтрока&gt; - Строковое выражение - командная строка запуска</p>
<p>&lt;ПустаяСтрока&gt; - Строковое выражение. Может содержать пустую строку или строковое значение "NO_SPLASH_SHOW" - отключить заставку при запуске системы. </p>

<p>Метод Initialize возвратит значение логического типа: TRUE, если инициализация прошла удачно, или FALSE в противном случае. Следует иметь в виду, что в OLE Automation TRUE и FALSE имеют соответственно значения -1 (минус единица) и 0. </p>

<p>Параметры командной строки запуска подробно описаны в руководстве к программе "1С:Предприятие", здесь же я приведу лишь те, которые могут оказаться вам полезными:</p>
<p>/DПуть к базе - задает путь к базе программы.</p>
<p>/M - запуск программы в монопольном режиме</p>
<p>/NИмя пользователя</p>
<p>/PПароль - пароль указанного пользователя </p>

<p>Параметры, не указанные в командной строке, будут запрошены программой в диалоговом режиме. </p>

<p>Например, инициализация программы в монопольном режиме с явным указанием пути к базе данных (D:\buh2001test), имени пользователя (Саша) и пароля (12345) без вывода на экран заставки выполняется следующим образом (здесь и далее подразумевается, что объект onesobj уже создан оператором createoleobject): </p>

<p>onesobj.initialize(onesobj.rmtrade,'/DD:\buh2001test /M /NСаша /P12345','NO_SPLASH_SHOW'); </p>

<p>В отличие от, например, OLE Automation-сервера приложения Microsoft Excel, сервер программы "1С-Предприятие" запускается в режиме "hide", то есть рабочее окно программы не отображается на экране. </p>

<p>Для использования созданного и проинициализированного объекта необходимо просто обращаться к атрибутам и методам системы 1С:Предприятие как OLE Automation сервера. </p>

<p>Для завершения работы с программой необходимо освободить OLE-объект путем присвоения ему значения UnAssigned: </p>

<p>onesobj := UnAssigned; </p>

<p>Впрочем, это необязательно, так как при закрытии вашего приложения OLE-объект будет освобожден автоматически. </p>

<p>Просуммируем полученные знания: создадим OLE-объект "1С:Предприятие", проинициализируем его и корректно освободим: </p>
<pre name="code" class="delphi">
procedure TForm1.Create1C;
var
  onesobj: Olevariant;
begin
  onesobj := createoleobject('V1CEnterprise.Application');
  onesobj.initialize(onesobj.rmtrade,
    '/DD:\buh2001test /M /NСаша /P12345', 'NO_SPLASH_SHOW');
  onesobj := UnAssigned;
end;
</pre>




<p>Как работать с полученным объектом </p>

<p>Резонный вопрос. Собственно, ради этого все и затевалось, не так ли? :) На самом деле, все очень просто. После того, как мы создали и проинициализировали OLE-объект, работать с ним можно следующим образом:</p>

<p>С помощью метода EvalExpr(&lt;СтрокаВыражения&gt;)</p>
<p>Метод EvalExpr вычисляет выражение, записанное параметре &lt;СтрокаВыражения&gt; на встроенном языке 1С:Предприятие и возвращает результат вычисления. Результатом выражения может быть число, строка, дата или значение любого агрегатного типа данных. </p>
<p>С помощью метода CreateObject(&lt;ИмяАгрегатногоТипа&gt;)</p>
<p>Метод CreateObject создает объект агрегатного типа данных системы 1С:Предприятие и возвращает ссылку на него. Данная функция обычно используется одновременно с явным определением переменной типа OLEVariant и присвоением ей ссылки на объект агрегатного типа данных. </p>
<p>С помощью метода ExecuteBatch(&lt;СтрокаОператоров&gt;)</p>
<p>Метод ExecuteBatch выполняет последовательность операторов, записанную в параметре &lt;СтрокаОператоров&gt; на встроенном языке 1С:Предприятие. Метод возвращает -1, если последовательность операторов выполнена успешно, или 0 в противном случае. </p>
<p>Вызовом атрибутов и методов системы 1С:Предприятие как OLE Automation сервера</p>

<p>Здесь есть несколько подводных камней, о которых я сразу хочу предупредить:</p>

<p>При вызове атрибутов и методов системы 1С:Предприятие необходимо использовать их англоязычные синонимы (они указаны для каждого метода в книге "Описание встроенного языка") </p>
<p>Для создаваемого агрегатного типа данных в среде Delphi необходимо завести переменную типа OLEVariant </p>
<p>В случае, если вызываемый метод OLE-объекта не требует параметров (либо один из параметров является необязательным), в качестве параметра ему необходимо передавать EmptyParam (либо - для Delphi 3 - пустую строку). </p>
<p>Для обращения к русскоязычным идентификаторам объектов агрегатных типов (например, реквизитов справочников) следует использовать метод объекта агрегатного типа getattrib(&lt;ИмяАтрибута&gt;) для получения значения атрибута, и setattrib(&lt;ИмяАтрибута&gt;) для установки значения. </p>
<p>Для комплексной иллюстрации всего вышеописанного я приведу пример, в котором содержимое справочника "Номенклатура" целиком экспортируется в таблицу базы данных (в примере подразумевается, что уже создана таблица table1, поля которой адекватны справочнику. Таблица table2 ссылается на ту же физическую таблицу, что и table1, и служит лишь для поиска уже добавленных элементов):</p>

<pre name="code" class="delphi">
procedure TForm1.exportsprav;
var
  counter: integer; //Счетчик импортированных записей
  onesobj: Olevariant; //OLE-объект программы 1С:Предприятие
  ware, ware2: olevariant; //Агрегатные объекты
  val, edizm, nds, np: olevariant;
  pf: integer; //Промежуточные переменные
begin
  table1.open; //Открываем таблицу1
  table2.open; //Открываем таблицу2
  counter := 0; //Обнуляем счетчик записей
  onesobj := createoleobject('V1CEnterprise.Application'); //Создаем OLE-объект
    //Инициализируем объект
  onesobj.initialize(onesobj.rmtrade, '/DD:\buh2001test /M /NСаша /P12345', 'NO_SPLASH_SHOW');
    //Создаем необходимые агрегатные объекты
  ware := onesobj.createobject('Справочник.Номенклатура');
  ware2 := onesobj.createobject('Справочник.Номенклатура');
  edizm := onesobj.createobject('Справочник.ЕдиницыИзмерений');
  nds := onesobj.createobject('Справочник.СтавкиНДС');
  np := onesobj.createobject('Справочник.СтавкиНП');
  ware.selectgroup(1); //Устанавливаем режим выборки групп
  ware.selectitems(1); //Открываем выборку элементов справочника
  while ware.GetItem(1) &gt; 0 do //Выбираем все элементы
  begin
    if ware.level('') = 1 then //Если мы выбрали группу первого уровня, то
      pf := -1
    else
    begin
            //Иначе ищем элемент-родитель
      ware2.FindItem(ware.getattrib('Родитель'));
      if table2.findkey([ware2.getattrib('Код')]) then
                //Если этот элемент мы уже импортировали
        pf := table2.fieldbyname('ID').AsInteger //, то получаем его код
      else
        pf := -1; //иначе помещаем элемент в группу первого уровня
    end;
    if ware.deletemark('') = 0 then //Если элемент не удален, то
    begin
      table1.append; //добавляем новое поле к таблице
            //Заполняем поля таблицы значениями соответствующих атрибутов элемента справочника
      table1.fieldbyname('CODE_1S').AsInteger := ware.getAttrib('Код');
            //Заполняем поле наименования
      table1.fieldbyname('NAME').AsString := ware.getAttrib('Наименование');
      table1.fieldbyname('PARENT_FOLDER').AsInteger := pf;
      table1.fieldbyname('FULLNAME').AsString := ware.getAttrib('ПолнНаименование');
            //Ищем соответствующую запись в справочнике "единицы измерения"
      edizm.finditem(ware.getattrib('ЕдиницаИзмерения'));
            //Заполняем поле единицы измерения
      table1.fieldbyname('EDIZM').AsString := edizm.getattrib('Наименование');
            //так мы получаем значения периодических реквизитов
      table1.fieldbyname('SEBESTOIM').AsFloat :=
        ware.getAttrib('Себестоимость').GetValue(datetostr(now));
      table1.fieldbyname('PRICEOPT').AsFloat := ware.getAttrib('Цена');
      nds.finditem(ware.getAttrib('СтавкаНДС').GetValue(datetostr(now)));
      np.finditem(ware.getAttrib('СтавкаНП').GetValue(datetostr(now)));
            //Заполняем поле ставки НДС
      table1.fieldbyname('STNDS').AsFloat := nds.getAttrib('Ставка');
            //Заполняем поле ставки НП
      table1.fieldbyname('STNP').AsFloat := np.getAttrib('Ставка');
      table1.fieldbyname('ARTICUL').AsString := ware.getAttrib('Артикул');
      if Ware.IsGroup('') = 1 then //Если мы выбрали группу товара, то
        table1.fieldbyname('IS_FOLDER').AsInteger := 1
      else
        table1.fieldbyname('IS_FOLDER').AsInteger := 0;
      table1.post;
      table2.refresh;
    end;
    inc(counter);
  end;
end;
</pre>



<p>Заключение </p>

<p>К сожалению, невозможно вместить в одну статью всю информацию, которая была бы вам полезна. Я постарался дать лишь тот минимум, который необходим для получения некоторых базовых знаний, и способен стать фундаментом для ваших собственных маленьких открытий в области интеграции Delphi и "1С:Предприятие". </p>

<p>Пишите мне, задавайте вопросы, и вполне возможно, что вскоре появится продолжение статьи. </p><p>Взято с <a href="http://delphiworld.narod.ru" target="_blank">http://delphiworld.narod.ru</a></p>
</div>
<!-- Actual content end -->
<hr />
<div id="footer">
<p>&copy; DRKB Library, 2010<br />Разработка и поддержка &mdash; <a href="http://www.drkb.ru/" target="_blank">Quadr0</a></p>
</div>
</div>
</body>
</html>
