<html>
<head>
  <title>Управляющие конструкции</title>
  <meta http-equiv="Content-Type" content="text/html; charset=windows-1251" />
  <link type="text/css" href="css/css.css" rel="stylesheet" />
  <link type="text/css" href="css/sh.css" rel="stylesheet" />
  <script language="javascript" src="js/shInit.js"></script>
  <script language="javascript" src="js/shCore.js"></script>
  <script language="javascript" src="js/shBrushDelphi.js"></script>
  <script language="javascript" src="js/shBrushSql.js"></script>
</head>
<body>
<div id="layout">
<div id="header">
  <div id="logo"><img id="logo" src="img/logo.png" /></div>
  <div id="navigation">
    <p id="navigation">
      <a class="navigation" onclick="prev();" href="#">Предыдущая</a><br />
      <a class="navigation" onclick="up();" href="#">Наверх</a><br />
      <a class="navigation" onclick="next();" href="#">Следующая</a>
    </p>
  </div>
</div>
<div id="content">
<div id="explorer">DRKB Explorer</div>
  <h1 id="title">Управляющие конструкции</h1>
<div id="date">01.01.2010</div>
<!-- Actual content start -->

<p>5. Управляющие конструкции</p>
ВВЕДЕНИЕ </p>
В четырех первых главах этой серии мы сконцентрировали свое внимание на синтаксическом анализе математических выражений и операций присваивания. В этой главе мы остановимся на новой и захватывающей теме: синтаксическом анализе и трансляции управляющих конструкций таких как, например, операторы IF. </p>
Эта тема дорога для моего сердца, потому что является для меня поворотной точкой. Я играл с синтаксическим анализом выражений также как мы делали это в этой серии, но я все же чувствовал,  что нахожусь еще очень далеко от возможности поддержки полного языка. В конце концов, реальные языки имеют ветвления, циклы, подпрограммы и все такое. Возможно вы разделяли некоторые из этих мыслей. Некоторое время назад, тем не менее, я  должен был реализовать управляющие конструкции для структурного препроцессора ассемблера, который я писал. Вообразите мое удивление, когда я обнаружил, что это было гораздо проще, чем синтаксический анализ выражений, через который я уже прошел. Я помню подумал "Эй, это же просто!". После того, как мы закончим этот урок, я готов поспорить, что вы будете думать так же. </p>
ПЛАН </p>
Далее мы снова начнем с пустого Cradle и, как мы делали уже дважды до этого, будем строить программу последовательно. Мы также сохраним концепцию одно-символьных токенов, которая так хорошо служила нам до настоящего времени. Это означает, что "код" будет выглядеть немного забавным с "i" вместо IF, "w" вместо WHILE и т.д. Но это поможет нам узнать основные понятия  не беспокоясь о лексическом анализе. Не бойтесь... в конечном счете мы увидим что-то похожее на "настоящий" код. </p>
Я также не хочу, чтобы мы увязли в работе с какими либо операторами кроме ветвлений, такими как операции присваивания, с которыми мы уже работали. Мы уже показали, что можем обрабатывать их, так что нет никакого смысла таскать этот лишний багаж в течение предстоящих занятий. Вместо этого я буду использовать анонимный оператор "other" для замены не управляющих операторов. Мы должны генерировать для них некоторый объектный код (мы возвращаемся к компиляции а не интерпретации), так что за неимением чего-либо другого я буду просто повторять входной символ. </p>
Итак, тогда, начав с еще одной копии Cradle, давайте определим процедуру: </p>
<pre name="code" class="delphi">
{ Recognize and Translate an "Other" } 
procedure Other; 
begin 
   EmitLn(GetName); 
end; 
</pre>
&nbsp;</p>
Теперь включим ее вызов в основную программу таким образом: </p>
<pre name="code" class="delphi">
{ Main Program } 
begin 
   Init; 
   Other; 
end.
</pre>
&nbsp;</p>
&nbsp;</p>
Запустите программу и посмотрите, что вы получили. Не очень захватывающе, не так ли? Но не зацикливайтесь на этом, это только начало, результат будет лучше. </p>
Первое, что нам нужно - это возможность работать с более чем одним оператором, так как однострочные ветвления довольно ограничены. Мы делали это на последнем занятии по интерпретации, но сейчас давайте будем немного более формальными. Рассмотрите следующую БНФ: </p>
     &lt;program&gt; ::= &lt;block&gt; END </p>
     &lt;block&gt; ::= [ &lt;statement&gt; ]* </p>
Это означает, что программа определена как блок, завершаемый утверждением END. Блок, в свою очередь, состоит из нуля или более операторов. Пока у нас есть только один вид операторов. </p>
Что является признаком окончания блока? Это просто любая конструкция, не являющаяся оператором "other". Сейчас это только утверждение END. </p>
Вооружившись этими идеями, мы можем приступать к созданию нашего синтаксического анализатора. Код для program (мы должны назвать его DoProgram, иначе Pascal будет ругаться) следующий: </p>
<pre name="code" class="delphi">
{ Parse and Translate a Program } 
procedure DoProgram; 
begin 
   Block; 
   if Look &lt;&gt; 'e' then Expected('End'); 
   EmitLn('END') 
end;
</pre>
&nbsp;</p>
Обратите внимание, что я выдаю ассемблеру команду "END", что своего рода расставляет знаки препинания в выходном коде и заставляет чувствовать, что мы анализируем здесь законченную программу. </p>
Код для Block: </p>
<pre name="code" class="delphi">
{ Recognize and Translate a Statement Block } 
procedure Block; 
begin 
   while not(Look in ['e']) do begin 
      Other; 
   end; 
end;
</pre>
&nbsp;</p>
(Из формы процедуры вы видите, что мы собираемся постепенно ее расширять!) </p>
ОК, вставьте эти подпрограммы в вашу программу. Замените вызов Block в основной программе на вызов DoProgram. Теперь испытайте ее и посмотрите как она работает. Хорошо, все еще не так много, но мы становимся все ближе. </p>
НЕМНОГО ОСНОВ </p>
Прежде чем мы начнем определять различные управляющие конструкции, мы должны положить немного более прочное основание. Во-первых, предупреждаю: я не буду использовать для этих конструкций тот же самый синтаксис с которым вы знакомы по Паскалю или Си. К примеру синтаксис Паскаль для IF такой: </p>
     IF &lt;condition&gt; THEN &lt;statement&gt; </p>
(где &lt;statement&gt;, конечно, может быть составным.) </p>
Синтаксис C аналогичен этому: </p>
     IF ( &lt;condition&gt; ) &lt;statement&gt; </p>
Вместо этого я буду использовать нечто более похожее на Ada: </p>
     IF &lt;condition&gt; &lt;block&gt; ENDIF </p>
Другими словами, конструкция IF имеет специфический символ завершения. Это позволит избежать висячих else Паскаля и Си и также предотвращает необходимость использовать скобки {} или begin-end. Синтаксис, который я вам здесь показываю, фактически является синтаксисом языка KISS, который я буду детализировать в следующих главах. Другие конструкции также будут немного отличаться. Это не должно быть для вас большой проблемой. Как только вы увидите, как это делается, вы поймете, что в действительности не имеет большого значения, какой конкретный синтаксис используется. Как только синтаксис определен, включить его в код достаточно просто. </p>
Теперь, все конструкции, с которыми мы будем иметь дело, включают передачу управления, что на уровне ассемблера означает условные и/или безусловные переходы. К примеру простой оператор IF: </p>
     IF &lt;condition&gt; A ENDIF B... </p>
должен быть переведен в: </p>
Если условие не выполнено то переход на L </p>
        A </p>
     L: B</p>
          ... </p>
Ясно, что нам понадобятся несколько процедур, которые помогут нам работать с этими переходами. Ниже я определил две из них. Процедура NewLabel генерирует уникальные метки. Это сделано с помощью простого способа называть каждую метку 'Lnn', где nn - это номер метки, начинающийся с нуля. Процедура PostLabel просто выводит метки в соответствующем месте. </p>
Вот эти две подпрограммы: </p>
<pre name="code" class="delphi">
{ Generate a Unique Label } 
function NewLabel: string; 
var S: string; 
begin 
   Str(LCount, S); 
   NewLabel := 'L' + S; 
   Inc(LCount); 
end; 
 
{ Post a Label To Output } 
procedure PostLabel(L: string); 
begin 
   WriteLn(L, ':'); 
end; 
</pre>
&nbsp;</p>
Заметьте, что мы добавили новую глобальную переменную LCount, так что вы должны изменить раздел описания переменных в начале программы, следующим образом: </p>
     var Look  : char;              { Lookahead Character } </p>
         Lcount: integer;           { Label Counter } </p>
Также добавьте следующий дополнительный инициализирующий код в Init: </p>
     LCount := 0; </p>
(Не забудьте сделать это, иначе ваши метки будут выглядеть действительно странными!). </p>
В этом месте я также хотел бы показать вам новый вид нотации. Если вы сравните форму оператора IF, указанную выше, с ассемблерным кодом, который должен быть получен, то вы можете увидеть, что существуют некоторые определенные действия, связанные с каждым ключевым словом в операторе: </p>
     IF: Сначала получить условие и выдать код для него. Затем создать уникальную метку и выдать переход если условие ложно. </p>
     ENDIF: Выдать метку. </p>
Эти действия могут быть показаны очень кратко, если мы запишем синтаксис таким образом: </p>
     IF </p>
     &lt;condition&gt; { Condition; <br>
                            L = NewLabel; <br>
                            Emit(Branch False to L); } <br>
<p>       &lt;block&gt;</p>
     ENDIF       { PostLabel(L) } </p>
Это пример синтаксически-управляемого перевода. Мы уже делали все это... мы просто никогда прежде не записывали это таким образом. Содержимое фигурных скобок представляет собой действия, которые будут выполняться. Хорошо в этом способе представления то, что он не только показывает что мы должны распознать, но также и действия, которые мы должны выполнить и в каком порядке. Как только мы получаем такой синтаксис, код возникает почти сам собой. </p>
Почти единственное, что осталось сделать - конкретизировать то, что мы подразумеваем под "Переход если условие ложно". </p>
Я полагаю, что должен быть код, выполняющийся для &lt;condition&gt;, который будет выполнять булеву алгебру и вычислять некоторый результат. Он также должен установить флажки условий, соответствующие этому результату. Теперь, обычным соглашением для булевых переменных является использование 0000 для представления значения "ложь" и какого-либо другого значения (кто-то использует FFFF, кто-то 0001) для представления "истины". </p>
Процессор 68000 устанавливает флажки условий всякий раз, когда любые данные перемещаются или рассчитываются. Если данные равны 0000 (что соответствует условию ложь, запомните) будет установлен флажок ноль. Код для "перехода по нулю" - BEQ. Таким образом </p>
     BEQ  &lt;=&gt; Переход если ложь </p>
     BNE  &lt;=&gt; Переход если истина </p>
По природе вещей большинство ветвлений, которые мы увидим, будут BEQ...  мы будем обходить вокруг кода, который должен выполняться когда условие истинно. </p>
ОПЕРАТОР IF </p>
После этого небольшого пояснения метода мы наконец готовы начать программирование синтаксического анализатора для условного оператора. Фактически, мы уже почти сделали это! Как обычно я буду использовать наш одно-символьный подход, с символом "i" вместо "IF" и "e" вместо "ENDIF" (также как и END... это двойственная природа не вызывает никакого беспорядка). Я также пока полностью пропущу символ для условия ветвления, который мы все еще должны определить. </p>
Код для DoIf: </p>
<pre name="code" class="delphi">
{ Recognize and Translate an IF Construct } 
procedure Block; Forward; 
procedure DoIf; 
var L: string; 
begin 
   Match('i'); 
   L := NewLabel; 
   Condition; 
   EmitLn('BEQ ' + L); 
   Block; 
   Match('e'); 
   PostLabel(L); 
end; 
</pre>
Добавьте эту подпрограмму в вашу программу и измените Block так, чтобы он ссылался на нее как показано ниже: </p>
<pre name="code" class="delphi">
{ Recognize and Translate a Statement Block } 
procedure Block; 
begin 
   while not(Look in ['e']) do begin 
      case Look of 
       'i': DoIf; 
       'o': Other; 
      end; 
   end; 
end; 
</pre>
&nbsp;</p>
Обратите внимание на обращение к процедуре Condition. В конечном итоге мы напишем подпрограмму, которая сможет анализировать и транслировать любое логическое условие которое мы ей дадим. Но это уже тема для отдельной главы (фактически следующей). А сейчас давайте просто заменим ее макетом, который выдает некоторый текст. Напишите следующую подпрограмму: </p>
<pre name="code" class="delphi">
{ Parse and Translate a Boolean Condition } 
{ This version is a dummy } 
Procedure Condition; 
begin 
   EmitLn('&lt;condition&gt;'); 
end;
</pre>
&nbsp;</p>
Вставьте эту процедуру в вашу программу как раз перед DoIf. Теперь запустите программу. Испробуйте строку типа: </p>
     aibece </p>
Как вы можете видеть, синтаксический анализатор, кажется, распознает конструкцию и вставляет объектный код в правильных местах. Теперь попробуйте набор вложенных IF: </p>
     aibicedefe </p>
Он начинает все более походить на настоящий, не так ли? </p>
Теперь, когда у нас есть общая идея (и инструменты, такие как нотация и процедуры NewLabel и PostLabel) проще пареной репы расширить синтаксический анализатор для поддержки и других конструкций. Первое (а также и одно из самых сложных) это добавление условия ELSE в IF. В БНФ это выглядит так: </p>
     IF &lt;condition&gt; &lt;block&gt; [ ELSE &lt;block&gt;] ENDIF </p>
Сложность возникает просто потому, что здесь присутствует необязательное условие, которого нет в других конструкциях. </p>
Соответствующий выходной код должен быть таким: </p>
          &lt;condition&gt; </p>
          BEQ L1 <br>
          &lt;block&gt; <br>
          BRA L2 <br>
<p>     L1:  &lt;block&gt; </p>
     L2: ... </p>
     Это приводит нас к следующей синтаксически управляемой схеме перевода: </p>
     IF </p>
     &lt;condition&gt;    { L1 = NewLabel; <br>
                      L2 = NewLabel; <br>
                      Emit(BEQ L1) } <br>
     &lt;block&gt; <br>
     ELSE           { Emit(BRA L2); <br>
                      PostLabel(L1) } <br>
<p>     &lt;block&gt; </p>
     ENDIF          { PostLabel(L2) } </p>
Сравнение этого со случаем IF без ELSE дает нам понимание того, как обрабатывать обе эти ситуации. Код ниже выполняет это. (Обратите внимание, что  использую "l"  вместо "ELSE" так как "e" имеет другое назначение): </p>
<pre name="code" class="delphi">
{ Recognize and Translate an IF Construct } 
procedure DoIf; 
var L1, L2: string; 
begin 
   Match('i'); 
   Condition; 
   L1 := NewLabel; 
   L2 := L1; 
   EmitLn('BEQ ' + L1); 
   Block; 
   if Look = 'l' then begin 
      Match('l'); 
      L2 := NewLabel; 
      EmitLn('BRA ' + L2); 
      PostLabel(L1); 
      Block; 
   end; 
   Match('e'); 
   PostLabel(L2); 
end; 
</pre>
&nbsp;</p>
Вы получили его. Законченный анализатор/транслятор в 19 строк кода. </p>
Сейчас протестируйте его. Испробуйте что-нибудь типа: </p>
     aiblcede </p>
Работает? Теперь, только для того, чтобы убедиться, что мы ничего не испортили и случай с IF без ELSE тоже будет обрабатываться, введите </p>
     aibece </p>
Теперь испробуйте несколько вложенных IF. Испытайте что-нибудь на ваш выбор, включая несколько неправильных утверждений. Только запомните, что 'e' не является допустимым оператором "other". </p>
ОПЕРАТОР WHILE </p>
Следующий вид оператора должен быть простым, так как мы уже имеем опыт. Синтаксис, который я выбрал для оператора WHILE следующий: </p>
     WHILE &lt;condition&gt; &lt;block&gt; ENDWHILE </p>
Знаю, знаю, мы действительно не нуждаемся в отдельных видах ограничителей для каждой конструкции... вы можете видеть, что фактически в нашей одно-символьной версии 'e' используется для всех из них. Но я также помню множество сессий отладки в Паскале, пытаясь отследить своенравный END который по мнению компилятора я хотел поместить где-нибудь еще. По своему опыту знаю, что специфичные и уникальные ключевые слова, хотя они и добавляются к словарю языка, дают небольшую защиту от ошибок, которая стоит дополнительной работы создателей компиляторов. </p>
Теперь рассмотрите, во что должен транслироваться WHILE: </p>
<pre name="code" class="delphi">
     L1:  &lt;condition&gt; 
          BEQ L2 
          &lt;block&gt; 
          BRA L1 
     L2: 
</pre>
&nbsp;</p>
Как и прежде, сравнение этих двух представлений дает нам действия, необходимые на каждом этапе: </p>
     WHILE          { L1 = NewLabel; </p>
                      PostLabel(L1) } <br>
     &lt;condition&gt;    { Emit(BEQ L2) } <br>
     &lt;block&gt; <br>
<p>     ENDWHILE  &nbsp;&nbsp;&nbsp;&nbsp; { Emit(BRA L1); </p>
                      PostLabel(L2) } </p>
Код выходит непосредственно из синтаксиса: </p>
<pre name="code" class="delphi">
{ Parse and Translate a WHILE Statement } 
procedure DoWhile; 
var L1, L2: string; 
begin 
   Match('w'); 
   L1 := NewLabel; 
   L2 := NewLabel; 
   PostLabel(L1); 
   Condition; 
   EmitLn('BEQ ' + L2); 
   Block; 
   Match('e'); 
   EmitLn('BRA ' + L1); 
   PostLabel(L2); 
end;  
</pre>
&nbsp;</p>
Так как мы получили новый оператор, мы должны добавить его вызов в процедуру Block: </p>
<pre name="code" class="delphi">
{ Recognize and Translate a Statement Block } 
procedure Block; 
begin 
   while not(Look in ['e', 'l']) do begin 
      case Look of 
       'i': DoIf; 
       'w': DoWhile; 
       else Other; 
      end; 
   end; 
end;
</pre>
&nbsp;</p>
Никаких других изменений не требуется. </p>
Хорошо, протестируйте новую программу. Заметьте, что на этот раз код &lt;condition&gt; находится внутри верхней метки, как раз там, где нам надо. Попробуйте несколько вложенных циклов. Испробуйте циклы внутри IF и IF внутри циклов. Если вы немного напутаете то, что вы должны набирать, не смущайтесь:  вы пишите ошибки и в других языках, не правда ли?  Код будет выглядеть более осмысленным, когда мы получим полные ключевые слова. </p>
Я надеюсь, что к настоящему времени вы начинаете понимать, что это действительно просто. Все, что нам необходимо было сделать для того, чтобы создать новую конструкцию, это разработать ее синтаксически-управляемый перевод. Код возникает из него, и это не влияет на другие подпрограммы. Как только вы почувствуете это, вы увидите, что можете добавлять новые конструкции почти также быстро, как вы можете их придумывать. </p>
ОПЕРАТОР LOOP </p>
Мы могли бы остановиться на этом и иметь работающий язык. Много раз было показано, что языка высокого уровня всего с двумя конструкциями IF и WHILE достаточно для написания структурного кода. Но раз уж мы начали, то давайте немного расширим репертуар. </p>
Эта конструкция даже проще, так как она совсем не имеет проверки условия... это бесконечный цикл. Имеет ли смысл такой цикл? Немного сам по себе, но позднее мы собираемся добавить команду BREAK, которая даст нам способ выхода из цикла. Она делает язык значительно более богатым, чем Паскаль, который не имеет команды выхода из цикла и также позволяет избежать забавных конструкций типа WHILE(1) или WHILE TRUE в C и Паскале. </p>
Синтаксис прост: </p>
     LOOP &lt;block&gt; ENDLOOP </p>
Синтаксически управляемый перевод: </p>
     LOOP           { L = NewLabel; </p>
                      PostLabel(L) } <br>
<p>     &lt;block&gt; </p>
     ENDLOOP        { Emit(BRA L } </p>
Соответствующий код показан ниже. Так как мы уже использовали "l" для ELSE на этот раз я использовал последнюю букву "p" как "ключевое слово". </p>
<pre name="code" class="delphi">
{ Parse and Translate a LOOP Statement } 
procedure DoLoop; 
var L: string; 
begin 
   Match('p'); 
   L := NewLabel; 
   PostLabel(L); 
   Block; 
   Match('e'); 
   EmitLn('BRA ' + L); 
end;
</pre>
&nbsp;</p>
После того, как вы вставите эту подпрограмму, не забудьте добавить строчку в Block для ее вызова. </p>
REPEAT-UNTIL </p>
Имеется одна конструкция, которую я взял напрямую из Паскаля. Синтаксис: </p>
     REPEAT &lt;block&gt; UNTIL &lt;condition&gt; </p>
и синтаксически-управляемый перевод: </p>
     REPEAT         { L = NewLabel;</p>
                      PostLabel(L) } <br>
     &lt;block&gt; <br>
<p>     UNTIL </p>
     &lt;condition&gt;    { Emit(BEQ L) } </p>
Как обычно, код вытекает отсюда довольно легко: </p>
<pre name="code" class="delphi">
{ Parse and Translate a REPEAT Statement } 
procedure DoRepeat; 
var L: string; 
begin 
   Match('r'); 
   L := NewLabel; 
   PostLabel(L); 
   Block; 
   Match('u'); 
   Condition; 
   EmitLn('BEQ ' + L); 
end; 
</pre>
&nbsp;</p>
&nbsp;</p>
&nbsp;</p>
Как и прежде, мы должны добавить вызов DoRepeat в Block. Хотя на этот раз есть различия. Я решил использовать "r" вместо REPEAT (естественно), но я также решил использовать "u" вместо UNTIL. Это означает, что "u" должен быть добавлен к множеству символов в условии while. Это символы, которые сигнализируют о выходе из текущего блока... символы "follow", на жаргоне разработчиков компиляторов. </p>
<pre name="code" class="delphi">
{ Recognize and Translate a Statement Block } 
procedure Block; 
begin 
   while not(Look in ['e', 'l', 'u']) do begin 
      case Look of 
       'i': DoIf; 
       'w': DoWhile; 
       'p': DoLoop; 
       'r': DoRepeat; 
       else Other; 
      end; 
   end; 
end;
</pre>
&nbsp;<br>
<p>&nbsp;</p>
&nbsp;</p>
ЦИКЛ FOR </p>
Цикл FOR очень удобен, но он тяжел для трансляции. Не столько потому, что сама конструкция трудна... в конце концов это всего лишь цикл... но просто потому, что она трудна для реализации на ассемблере. Как только код придуман, трансляция достаточно проста. </p>
Фаны Си любят цикл FOR этого языка (фактически он проще для кодирования), но вместо него я выбрал синтаксис очень похожий на синтаксис из старого доброго Бейсика: </p>
     FOR &lt;ident&gt; = &lt;expr1&gt; TO &lt;expr2&gt; &lt;block&gt; ENDFOR </p>
Сложность трансляции цикла "FOR" зависит от выбранного вами способа его реализации, от пути, которым вы решили определять правила обработки ограничений. Рассчитывается ли expr2 каждый раз при прохождении цикла, например, или оно обрабатывается как постоянное ограничение? Всегда ли вы проходите цикл хотя бы раз, как в Fortran, или нет. Все становится проще, если вы приверженец точки зрения что эта конструкция эквивалентна: </p>
     &lt;ident&gt; = &lt;expr1&gt; </p>
     TEMP = &lt;expr2&gt; <br>
     WHILE &lt;ident&gt; &lt;= TEMP <br>
<p>     &lt;block&gt; </p>
     ENDWHILE </p>
Заметьте, что с этим определением цикла &lt;block&gt; не будет выполнен вообще если &lt;expr1&gt; изначально больше чем &lt;expr2&gt;. </p>
Код 68000, необходимый для этого, сложней чем все что мы делали до сих пор. Я сделал несколько попыток,  помещая и счетчик и верхний предел в стек, в регистры и т.д. В конечном итоге я остановился на гибридном варианте размещения, при котором счетчик помещается в памяти (поэтому он может быть доступен внутри цикла) а верхний предел - в стеке. Оттранслированный код получился следующий: </p>
<pre name="code" class="delphi">
          &lt;ident&gt;   ; получить имя счетчика цикла 
          &lt;expr1&gt;   ; получить начальное значение 
          LEA &lt;ident&gt;(PC),A0 ; обратиться к счетчику цикла 
          SUBQ #1,D0  ; предварительно уменьшить его 
          MOVE D0,(A0)  ; сохранить его 
          &lt;expr1&gt;   ; получить верхний предел 
          MOVE D0,-(SP)  ; сохранить его в стеке 
      L1: LEA &lt;ident&gt;(PC),A0 ; обратиться к счетчику цикла 
          MOVE (A0),D0  ; извлечь его в D0 
          ADDQ #1,D0  ; увеличить счетчик 
          MOVE D0,(A0)  ; сохранить новое значение 
          CMP (SP),D0  ; проверить диапазон 
          BLE L2   ; пропустить если D0 &gt; (SP) 
          &lt;block&gt; 
          BRA L1   ; цикл для следующего прохода 
      L2: ADDQ #2,SP  ; очистить стек 
</pre>
&nbsp;</p>
Ничего себе! Это же куча кода... строка, содержащая &lt;block&gt;, кажется совсем потерявшейся. Но это лучшее из того, что я смог придумать. Я полагаю, чтобы вам помочь, вы должны иметь в виду что в действительности это всего лишь шестнадцать слов, в конце концов. Если кто-нибудь сможет оптимизировать это лучше, пожалуйста дайте мне знать. </p>
Однако, подпрограмма анализа довольно проста теперь, когда у нас есть код: </p>
<pre name="code" class="delphi">
{ Parse and Translate a FOR Statement } 
procedure DoFor; 
var L1, L2: string; 
    Name: char; 
begin 
   Match('f'); 
   L1 := NewLabel; 
   L2 := NewLabel; 
   Name := GetName; 
   Match('='); 
   Expression; 
   EmitLn('SUBQ #1,D0'); 
   EmitLn('LEA ' + Name + '(PC),A0'); 
   EmitLn('MOVE D0,(A0)'); 
   Expression; 
   EmitLn('MOVE D0,-(SP)'); 
   PostLabel(L1); 
   EmitLn('LEA ' + Name + '(PC),A0'); 
   EmitLn('MOVE (A0),D0'); 
   EmitLn('ADDQ #1,D0'); 
   EmitLn('MOVE D0,(A0)'); 
   EmitLn('CMP (SP),D0'); 
   EmitLn('BGT ' + L2); 
   Block; 
   Match('e'); 
   EmitLn('BRA ' + L1); 
   PostLabel(L2); 
   EmitLn('ADDQ #2,SP'); 
end; 
</pre>
&nbsp;</p>
&nbsp;</p>
&nbsp;</p>
Так как в этой версии синтаксического анализатора у нас нет выражений, я использовал тот же самый прием что и для Condition и написал подпрограмму: </p>
&nbsp;</p>
{ Parse and Translate an Expression } <br>
<p>{ This version is a dummy } </p>
Procedure Expression; <br>
begin <br>
   EmitLn('&lt;expr&gt;'); <br>
<p>end; </p>
&nbsp;</p>
Испытайте его. Снова, не забудьте добавить вызов в Block. Так как у нас нет возможности ввода для фиктивной версии Expression, типичная входная строка будет выглядеть так: </p>
     afi=bece </p>
Хорошо, генерируется много кода, не так ли? Но, по крайней мере, это правильный код. </p>
ОПЕРАТОР DO </p>
Из-за всего этого мне захотелось иметь более простую версию цикла FOR. Причина появления всего этого кода выше состоит в необходимости иметь счетчик цикла, доступный как переменная внутри цикла. Если все, что нам нужно это считающий цикл, позволяющий нам выполнить что-то определенное число раз, но не нужен непосредственный доступ к счетчику, имеется более простое решение. Процессор 68000 имеет встроенную команду "уменьшить и переход если не ноль", которая является идеальной для подсчета. Для полноты давайте добавим и эту конструкцию. Это будет наш последний цикл. </p>
Синтаксис и его перевод: </p>
     DO </p>
     &lt;expr&gt;         { Emit(SUBQ #1,D0); </p>
                      L = NewLabel; </p>
                      PostLabel(L); </p>
                      Emit(MOVE D0,-(SP) } </p>
 &nbsp;   &lt;block&gt; </p>
     ENDDO          { Emit(MOVE (SP)+,D0; </p>
                      Emit(DBRA D0,L) } </p>
Это гораздо проще! Цикл будет выполняться &lt;expr&gt; раз. Вот код: </p>
<pre name="code" class="delphi">
{ Parse and Translate a DO Statement } 
procedure Dodo; 
var L: string; 
begin 
   Match('d'); 
   L := NewLabel; 
   Expression; 
   EmitLn('SUBQ #1,D0'); 
   PostLabel(L); 
   EmitLn('MOVE D0,-(SP)'); 
   Block; 
   EmitLn('MOVE (SP)+,D0'); 
   EmitLn('DBRA D0,' + L); 
end; 
</pre>
&nbsp;</p>
&nbsp;</p>
&nbsp;</p>
Я думаю вы согласитесь, что это гораздо проще, чем классический цикл FOR. Однако, каждая конструкция имеет свое назначение. </p>
ОПЕРАТОР BREAK </p>
Ранее я обещал вам оператор BREAK для сопровождения цикла LOOP. Им я в некотором роде горд. На первый взгляд BREAK кажется действительно сложным. Моим первым подходом было просто использовать его как дополнительный ограничитель в Block и разделить все циклы на две части точно также как я сделал это для ELSE оператора IF. Но, оказывается, это не работает, потому что оператор BREAK редко находится на том же самом уровне, что и сам цикл. Наиболее вероятное место для BREAK - сразу после IF, что приводило бы к выходу из конструкции IF, а не из окружающего цикла. Неправильно. BREAK должен выходить из внутреннего LOOP даже если он вложен в несколько уровней IF. </p>
Моей следующей мыслью было просто сохранять в какой-то глобальной переменной, метку окончания самого вложенного цикла. Это также не работает, потому что может возникнуть прерывание из внутреннего цикла с последующим прерыванием из внешнего. Сохранение метки для внутреннего цикла затерло бы метку для внешнего. Так глобальная переменная превратилась в стек. Дело становилось грязным. </p>
Тогда я решил последовать своему собственному совету. Помните последний урок, когда я показал вам как хорошо служит нам неявный стек синтаксического анализатора с рекурсивным спуском. Я сказал, что если вы начинаете видеть потребность во внешнем стеке, возможно вы делаете что-то неправильно. Действительно возможно заставить рекурсию, встроенную в наш синтаксический анализатор, позаботиться обо всем и это решение настолько простое, что кажется удивительным. </p>
Секрет состоит в том, чтобы заметить, что каждый оператор BREAK должен выполняться внутри блока... и ни в каком другом месте. Так что все, что мы должны сделать это передать в Block адрес выхода из самого внутреннего цикла. Затем он может передать этот адрес подпрограмме, транслирующей инструкцию Break. Так как оператор IF не изменяет уровень цикла, процедура DoIf не должна делать что-либо за исключением передачи метки в ее блок (оба из них). Так как циклы изменяют уровень, каждый цикл просто игнорирует любую метку выше его  и передает свою собственную метку выхода дальше. </p>
Все это проще показать вам чем описывать. Я продемонстрирую это с самым простым циклом, циклом LOOP: </p>
<pre name="code" class="delphi">
{ Parse and Translate a LOOP Statement } 
procedure DoLoop; 
var L1, L2: string; 
begin 
   Match('p'); 
   L1 := NewLabel; 
   L2 := NewLabel; 
   PostLabel(L1); 
   Block(L2); 
   Match('e'); 
   EmitLn('BRA ' + L1); 
   PostLabel(L2); 
end; 
</pre>
Заметьте, что теперь DoLoop имеет две метки а не одну. Вторая дает команде BREAK адрес перехода.&nbsp; Если в цикле нет BREAK, то мы зря потратили метку и немного загромоздили код, но не нанесли никакого вреда. </p>
Заметьте также, что процедура Block теперь имеет параметр, который для циклов всегда будет адресом выхода. Новая версия Block: </p>
<pre name="code" class="delphi">
{ Recognize and Translate a Statement Block } 
procedure Block(L: string); 
begin 
   while not(Look in ['e', 'l', 'u']) do begin 
      case Look of 
       'i': DoIf(L); 
       'w': DoWhile; 
       'p': DoLoop; 
       'r': DoRepeat; 
       'f': DoFor; 
       'd': DoDo; 
       'b': DoBreak(L); 
       else Other; 
      end; 
   end; 
end; 
</pre>
&nbsp;</p>
&nbsp;</p>
&nbsp;</p>
Снова заметьте, что все что Block делает с меткой это передает ее в DoIf и DoBreak. Циклы не нуждаются в ней, потому что они в любом случае передают свою собственную метку. </p>
Новая версия DoIf: </p>
<pre name="code" class="delphi">
{ Recognize and Translate an IF Construct } 
procedure Block(L: string); Forward; 
procedure DoIf(L: string); 
var L1, L2: string; 
begin 
   Match('i'); 
   Condition; 
   L1 := NewLabel; 
   L2 := L1; 
   EmitLn('BEQ ' + L1); 
   Block(L); 
   if Look = 'l' then begin 
      Match('l'); 
      L2 := NewLabel; 
      EmitLn('BRA ' + L2); 
      PostLabel(L1); 
      Block(L); 
   end; 
   Match('e'); 
   PostLabel(L2); 
end; 
</pre>
&nbsp;</p>
Здесь единственное, что изменяется, это добавляется параметр у процедуры Block. Оператор IF не меняет уровень вложенности цикла, поэтому DoIf просто передает метку дальше. Независимо от того, сколько уровней вложенности IF мы имеем, будет использоваться та же самая метка. </p>
Теперь не забудьте, что DoProgram также вызывает Block и теперь необходимо передавать ей метку. Попытка выхода из внешнего блока является ошибкой, поэтому DoProgram передает пустую метку, которая перехватывается DoBreak: </p>
<pre name="code" class="delphi">
{ Recognize and Translate a BREAK } 
procedure DoBreak(L: string); 
begin 
   Match('b'); 
   if L &lt;&gt; '' then 
      EmitLn('BRA ' + L) 
   else Abort('No loop to break from'); 
end; 
 
{ Parse and Translate a Program } 
procedure DoProgram; 
begin 
   Block(''); 
   if Look &lt;&gt; 'e' then Expected('End'); 
   EmitLn('END') 
end; 
</pre>
Этот код позаботится почти обо всем. Испытайте его, посмотрите, сможете ли вы "сломать" ("break") его (каламбур).  Аккуратней однако. К настоящему времени мы использовали так много букв, что трудно придумать символ, который не представляет сейчас какое либо зарезервированное слово. Не забудьте, перед тем, как вы протестируете программу, вы должны будете исправить каждый случай появления Block в других циклах для включения нового параметра. Сделайте это точно так же, как я сделал это для LOOP. </p>
Я сказал выше "почти". Есть одна небольшая проблема: если вы внимательно посмотрите на код, генерируемый для DO, вы увидите, что если вы прервете этот цикл, то значение счетчика все еще остается в стеке. Мы должны исправить это! Позор... это была одна из самых маленьких наших подпрограмм, но это не помогло. Вот новая версия, которая не имеет этой проблемы: </p>
<pre name="code" class="delphi">
{ Parse and Translate a DO Statement } 
procedure Dodo; 
var L1, L2: string; 
begin 
   Match('d'); 
   L1 := NewLabel; 
   L2 := NewLabel; 
   Expression; 
   EmitLn('SUBQ #1,D0'); 
   PostLabel(L1); 
   EmitLn('MOVE D0,-(SP)'); 
   Block(L2); 
   EmitLn('MOVE (SP)+,D0'); 
   EmitLn('DBRA D0,' + L1); 
   EmitLn('SUBQ #2,SP'); 
   PostLabel(L2); 
   EmitLn('ADDQ #2,SP'); 
end; 
</pre>
&nbsp;</p>
&nbsp;</p>
&nbsp;</p>
Две дополнительные инструкции SUBQ и ADDQ заботятся о сохранении стека в правильной форме. </p>
ЗАКЛЮЧЕНИЕ </p>
К этому моменту мы создали ряд управляющих конструкций... в действительности более богатый набор чем предоставляет почти любой другой язык программирования. И, за исключением цикла FOR, это было довольно легко сделать. Но даже этот цикл был сложен только потому, что сложность заключалась в ассемблере. </p>
Я завершаю на этом урок. Чтобы можно было обернуть наш продукт красной ленточкой, в действительности мы должны иметь настоящие ключевые слова вместо этих игрушечных одно-символьных. Вы уже видели, что расширить компилятор для поддержки много символьных слов не трудно, но в этом случае возникнут большие различия в представлении нашего входного кода. Я оставлю этот небольшой кусочек для следующей главы. В этой главе мы также рассмотрим логические выражения, так что мы сможем избавиться от фиктивной версии Condition, которую мы здесь использовали. Увидимся. </p>
Для справочных целей привожу полный текст синтаксического анализатора для этого урока: </p>
<pre name="code" class="delphi">
program Branch; 
 
{ Constant Declarations } 
const TAB = ^I; 
      CR  = ^M; 
 
{ Variable Declarations } 
var Look  : char;              { Lookahead Character } 
    Lcount: integer;           { Label Counter } 
 
{ Read New Character From Input Stream } 
procedure GetChar; 
begin 
   Read(Look); 
end; 
 
{ Report an Error } 
procedure Error(s: string); 
begin 
   WriteLn; 
   WriteLn(^G, 'Error: ', s, '.'); 
end; 
 
{ Report Error and Halt } 
procedure Abort(s: string); 
begin 
   Error(s); 
   Halt; 
end; 
 
{ Report What Was Expected } 
procedure Expected(s: string); 
begin 
   Abort(s + ' Expected'); 
end; 
 
{ Match a Specific Input Character } 
procedure Match(x: char); 
begin 
   if Look = x then GetChar 
   else Expected('''' + x + ''''); 
end; 
 
{ Recognize an Alpha Character } 
function IsAlpha(c: char): boolean; 
begin 
   IsAlpha := UpCase(c) in ['A'..'Z']; 
end; 
 
{ Recognize a Decimal Digit } 
function IsDigit(c: char): boolean; 
begin 
   IsDigit := c in ['0'..'9']; 
end; 
 
{ Recognize an Addop } 
function IsAddop(c: char): boolean; 
begin 
   IsAddop := c in ['+', '-']; 
end; 
 
{ Recognize White Space } 
function IsWhite(c: char): boolean; 
begin 
   IsWhite := c in [' ', TAB]; 
end; 
 
{ Skip Over Leading White Space } 
procedure SkipWhite; 
begin 
   while IsWhite(Look) do 
      GetChar; 
end; 
 
{ Get an Identifier } 
function GetName: char; 
begin 
   if not IsAlpha(Look) then Expected('Name'); 
   GetName := UpCase(Look); 
   GetChar; 
end; 
 
{ Get a Number } 
function GetNum: char; 
begin 
   if not IsDigit(Look) then Expected('Integer'); 
   GetNum := Look; 
   GetChar; 
end; 
 
{ Generate a Unique Label } 
function NewLabel: string; 
var S: string; 
begin 
   Str(LCount, S); 
   NewLabel := 'L' + S; 
   Inc(LCount); 
end; 
 
{ Post a Label To Output } 
procedure PostLabel(L: string); 
begin 
   WriteLn(L, ':'); 
end; 
 
{ Output a String with Tab } 
procedure Emit(s: string); 
begin 
   Write(TAB, s); 
end; 
 
{ Output a String with Tab and CRLF } 
procedure EmitLn(s: string); 
begin 
   Emit(s); 
   WriteLn; 
end; 
 
{ Parse and Translate a Boolean Condition } 
procedure Condition; 
begin 
   EmitLn('&lt;condition&gt;'); 
end; 
 
{ Parse and Translate a Math Expression } 
procedure Expression; 
begin 
   EmitLn('&lt;expr&gt;'); 
end; 
 
{ Recognize and Translate an IF Construct } 
procedure Block(L: string); Forward; 
procedure DoIf(L: string); 
var L1, L2: string; 
begin 
   Match('i'); 
   Condition; 
   L1 := NewLabel; 
   L2 := L1; 
   EmitLn('BEQ ' + L1); 
   Block(L); 
   if Look = 'l' then begin 
      Match('l'); 
      L2 := NewLabel; 
      EmitLn('BRA ' + L2); 
      PostLabel(L1); 
      Block(L); 
   end; 
   Match('e'); 
   PostLabel(L2); 
end; 
 
{ Parse and Translate a WHILE Statement } 
procedure DoWhile; 
var L1, L2: string; 
begin 
   Match('w'); 
   L1 := NewLabel; 
   L2 := NewLabel; 
   PostLabel(L1); 
   Condition; 
   EmitLn('BEQ ' + L2); 
   Block(L2); 
   Match('e'); 
   EmitLn('BRA ' + L1); 
   PostLabel(L2); 
end; 
 
{ Parse and Translate a LOOP Statement } 
procedure DoLoop; 
var L1, L2: string; 
begin 
   Match('p'); 
   L1 := NewLabel; 
   L2 := NewLabel; 
   PostLabel(L1); 
   Block(L2); 
   Match('e'); 
   EmitLn('BRA ' + L1); 
   PostLabel(L2); 
end; 
 
{ Parse and Translate a REPEAT Statement } 
procedure DoRepeat; 
var L1, L2: string; 
begin 
   Match('r'); 
   L1 := NewLabel; 
   L2 := NewLabel; 
   PostLabel(L1); 
   Block(L2); 
   Match('u'); 
   Condition; 
   EmitLn('BEQ ' + L1); 
   PostLabel(L2); 
end; 
 
{ Parse and Translate a FOR Statement } 
procedure DoFor; 
var L1, L2: string; 
    Name: char; 
begin 
   Match('f'); 
   L1 := NewLabel; 
   L2 := NewLabel; 
   Name := GetName; 
   Match('='); 
   Expression; 
   EmitLn('SUBQ #1,D0'); 
   EmitLn('LEA ' + Name + '(PC),A0'); 
   EmitLn('MOVE D0,(A0)'); 
   Expression; 
   EmitLn('MOVE D0,-(SP)'); 
   PostLabel(L1); 
   EmitLn('LEA ' + Name + '(PC),A0'); 
   EmitLn('MOVE (A0),D0'); 
   EmitLn('ADDQ #1,D0'); 
   EmitLn('MOVE D0,(A0)'); 
   EmitLn('CMP (SP),D0'); 
   EmitLn('BGT ' + L2); 
   Block(L2); 
   Match('e'); 
   EmitLn('BRA ' + L1); 
   PostLabel(L2); 
   EmitLn('ADDQ #2,SP'); 
end; 
 
{ Parse and Translate a DO Statement } 
procedure Dodo; 
var L1, L2: string; 
begin 
   Match('d'); 
   L1 := NewLabel; 
   L2 := NewLabel; 
   Expression; 
   EmitLn('SUBQ #1,D0'); 
   PostLabel(L1); 
   EmitLn('MOVE D0,-(SP)'); 
   Block(L2); 
   EmitLn('MOVE (SP)+,D0'); 
   EmitLn('DBRA D0,' + L1); 
   EmitLn('SUBQ #2,SP'); 
   PostLabel(L2); 
   EmitLn('ADDQ #2,SP'); 
end; 
 
{ Recognize and Translate a BREAK } 
procedure DoBreak(L: string); 
begin 
   Match('b'); 
   EmitLn('BRA ' + L); 
end; 
 
{ Recognize and Translate an "Other" } 
procedure Other; 
begin 
   EmitLn(GetName); 
end; 
 
{ Recognize and Translate a Statement Block } 
procedure Block(L: string); 
begin 
   while not(Look in ['e', 'l', 'u']) do begin 
      case Look of 
       'i': DoIf(L); 
       'w': DoWhile; 
       'p': DoLoop; 
       'r': DoRepeat; 
       'f': DoFor; 
       'd': DoDo; 
       'b': DoBreak(L); 
       else Other; 
      end; 
   end; 
end; 
 
{ Parse and Translate a Program } 
procedure DoProgram; 
begin 
   Block(''); 
   if Look &lt;&gt; 'e' then Expected('End'); 
   EmitLn('END') 
end; 
 
{ Initialize } 
procedure Init; 
begin 
   LCount := 0; 
   GetChar; 
end; 
 
{ Main Program } 
begin 
   Init; 
   DoProgram; 
end. 
</pre>
&nbsp;</p>
&nbsp;</p>
</div>
<!-- Actual content end -->
<hr />
<div id="footer">
<p>&copy; DRKB Library, 2010<br />Разработка и поддержка &mdash; <a href="http://www.drkb.ru/" target="_blank">Quadr0</a></p>
</div>
</div>
</body>
</html>
