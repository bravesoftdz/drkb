<html>
<head>
  <title>Список окон + определение приложения, создавшего эти окна</title>
  <meta http-equiv="Content-Type" content="text/html; charset=windows-1251" />
  <link type="text/css" href="css/css.css" rel="stylesheet" />
  <link type="text/css" href="css/sh.css" rel="stylesheet" />
  <script language="javascript" src="js/shInit.js"></script>
  <script language="javascript" src="js/shCore.js"></script>
  <script language="javascript" src="js/shBrushDelphi.js"></script>
  <script language="javascript" src="js/shBrushSql.js"></script>
</head>
<body>
<div id="layout">
<div id="header">
  <div id="logo"><img id="logo" src="img/logo.png" /></div>
  <div id="navigation">
    <p id="navigation">
      <a class="navigation" onclick="prev();" href="#">Предыдущая</a><br />
      <a class="navigation" onclick="up();" href="#">Наверх</a><br />
      <a class="navigation" onclick="next();" href="#">Следующая</a>
    </p>
  </div>
</div>
<div id="content">
<div id="explorer">DRKB Explorer</div>
  <h1 id="title">Список окон + определение приложения, создавшего эти окна</h1>
<div id="date">01.01.2010</div>
<!-- Actual content start -->

<pre name="code" class="delphi">
////////////////////////////////////////////////////////////////////////////////
//
//  Автор: Александр (Rouse_) Багель
//  © Fangorn Wizards Lab 1998 - 2002
//  16 октября 2002 18:21
 
//  Данный код приведен лишь для демонстрации
//  А простой вариант поиска Handle Ричедита выглядит так
//  var
//    Handle : HWND;
//  begin
//    Handle:= FindWindowEx(FindWindow(Название формы например 'Form1',nil), 0, Название элемента например 'Button1',  nil),  0, true);
 
unit Unit1;
 
interface
 
uses
  Windows, Messages, SysUtils, Classes, Graphics, Controls, Forms, Dialogs,
  ComCtrls;
 
type
  TMainForm = class(TForm)
    TreeView1: TTreeView;
    procedure FormCreate(Sender: TObject);
    procedure Sys_Windows_Tree(Node: TTreeNode; Handle: HWND);
  end;
 
var
  MainForm: TMainForm;
 
implementation
 
{$R *.DFM}
 
////////////////////////////////////////////////////////////////////////////////
//
//  Стартовая функция, запускаем рекуссию используя хэндл рабочего стола
//
 
procedure TMainForm.FormCreate(Sender: TObject);
var
  StartHandle : THandle;
begin 
  //Если требуется найти только данные по одному приложению
  //замени 2 строки в функциях их закоментированными аналогами
  StartHandle := GetDeskTopWindow;
  //StartHandle := 67324;//FindWindow(PChar(Caption), nil);  //На примере Винампа
  Sys_Windows_Tree(nil, StartHandle);
end;
 
////////////////////////////////////////////////////////////////////////////////
//
//  Рекурсивная функция, строит дерево всех открытых окон, кнопок, едитов и т.д.
//  В качестве входных данных получает узел дерева и Хэндл окна
//
 
procedure TMainForm.Sys_Windows_Tree(Node: TTreeNode; Handle: HWND);
const
  MAX = 128;
var
  TmpArray  : array[0..MAX - 1] of Char;
  Result    : String;
  szFileName : array[0..255] of Char;
  iSize : Integer;
  PID: Cardinal;
begin
  //Запускаем цикл пока не закончатся окна
  while Handle &lt;&gt; 0 do
  begin
    //Получаем имя класса окна
    GetClassName(Handle, @TmpArray, MAX);
    Result := String(TmpArray);
    //Получаем текст (Его Caption) окна
    GetWindowText(Handle, @TmpArray, MAX);
    // Получаем имя модуля
    if GetwindowModuleFilename(Handle, szFileName, SizeOf(szFileName)) = 0 then
      ZeroMemory(@szFileName[0], 256);
    GetWindowThreadProcessId(Handle, PID);
    Result := Result + ' [' + String(szFileName) + '] (' + String(TmpArray) +
      '): Handle = '+ IntToStr(Handle) + ', PID = ' + IntToStr(PID);
    //В следующей процедуре, в скобках, добавляем результат
    //в дерево, получаем хэндл дочернего окна и с результатами
    //выполнения этих двух функций выполняем процедуру Sys_Windows_Tree
    Sys_Windows_Tree(TreeView1.Items.AddChild(Node, Result),
      GetWindow(Handle, GW_CHILD));
    //Получаем хэндл следующего (не дочернего) окна
    Handle := GetNextWindow(Handle, GW_HWNDNEXT);
    //Handle := 0;
  end;
end; 
 
 
end.
</pre>

<p>&nbsp;<br>
<p>Взято из <a href="http://forum.sources.ru" target="_blank">http://forum.sources.ru</a></p>
Автор: Rouse_<br>

</div>
<!-- Actual content end -->
<hr />
<div id="footer">
<p>&copy; DRKB Library, 2010<br />Разработка и поддержка &mdash; <a href="http://www.drkb.ru/" target="_blank">Quadr0</a></p>
</div>
</div>
</body>
</html>
