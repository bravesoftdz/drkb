<html>
<head>
  <title>Плавающая панель</title>
  <meta http-equiv="Content-Type" content="text/html; charset=windows-1251" />
  <link type="text/css" href="css/css.css" rel="stylesheet" />
  <link type="text/css" href="css/sh.css" rel="stylesheet" />
  <script language="javascript" src="js/shInit.js"></script>
  <script language="javascript" src="js/shCore.js"></script>
  <script language="javascript" src="js/shBrushDelphi.js"></script>
  <script language="javascript" src="js/shBrushSql.js"></script>
</head>
<body>
<div id="layout">
<div id="header">
  <div id="logo"><img id="logo" src="img/logo.png" /></div>
  <div id="navigation">
    <p id="navigation">
      <a class="navigation" onclick="prev();" href="#">Предыдущая</a><br />
      <a class="navigation" onclick="up();" href="#">Наверх</a><br />
      <a class="navigation" onclick="next();" href="#">Следующая</a>
    </p>
  </div>
</div>
<div id="content">
<div id="explorer">DRKB Explorer</div>
  <h1 id="title">Плавающая панель</h1>
<div id="date">01.01.2010</div>
<!-- Actual content start -->
Кто-нибудь пробовал создать форму, подобную "отстегивающимся" панелькам (FreeDoc)? Я попробовал и вот что получилось... </p>
<p>Код требует использования некоторых функций WinAPI. Описание всех WinAPI функций доступны при нажатии F1 (электронная справка)... </p>
<p>Ну а теперь попробуем это создать (весь код занимает около 100 строчек)... </p>
<p>Ход работы: </p>
<p>Стартуйте новый проект, задайте свойству borderstyle формы значение bsNone, добавьте панель, установите у нее свойство borderstyle равным значению bsSingle, добавьте другую панель с любым заголовком, добавьте кнопку с подсказкой 'переключатель панели заголовка', вырежьте из данного совера код и вставьте его в модуль, создайте обработчики трех событий панелей (MouseDown, MouseMove, MouseUp) и один обработчик кнопки (Click). Надеюсь, что ничего не забыл... ;-) Быстрее сделать это в Delphi, чем написать здесь... ;-) </p>
<pre name="code" class="delphi">
unit Unit1;
 
interface
 
uses
  SysUtils, WinTypes, WinProcs, Messages, Classes, Graphics, Controls,
  Forms, Dialogs, ExtCtrls, StdCtrls;
 
type
  TForm1 = class(TForm)
    Panel1: TPanel;
    Panel2: TPanel;
    Button1: TButton;
    procedure Panel1MouseDown(Sender: TObject; Button: TMouseButton;
      Shift: TShiftState; X, Y: Integer);
    procedure Panel1MouseMove(Sender: TObject; Shift: TShiftState; X,
      Y: Integer);
    procedure Panel1MouseUp(Sender: TObject; Button: TMouseButton;
      Shift: TShiftState; X, Y: Integer);
    procedure Button1Click(Sender: TObject);
  private
    { Private declarations }
    OldX,
      OldY,
      OldLeft,
      OldTop: Integer;
    ScreenDC: HDC;
    MoveRect: TRect;
    Moving: Boolean;
  public
    { Public declarations }
  end;
 
var
 
  Form1: TForm1;
 
implementation
 
{$R *.DFM}
 
procedure TForm1.Panel1MouseDown(Sender: TObject; Button: TMouseButton;
 
  Shift: TShiftState; X, Y: Integer);
begin
 
  if Button = mbLeft then
  begin
    SetCapture(Panel1.Handle);
    ScreenDC := GetDC(0);
    OldX := X;
    OldY := Y;
    OldLeft := X;
    OldTop := Y;
    MoveRect := BoundsRect;
    DrawFocusRect(ScreenDC, MoveRect);
    Moving := True;
  end;
end;
 
procedure TForm1.Panel1MouseMove(Sender: TObject; Shift: TShiftState; X,
 
  Y: Integer);
begin
 
  if Moving then
  begin
    DrawFocusRect(ScreenDC, MoveRect);
    OldX := X;
    OldY := Y;
    MoveRect := Rect(Left + OldX - OldLeft, Top + OldY - OldTop,
      Left + Width + OldX - OldLeft, Top + Height + OldY - OldTop);
    DrawFocusRect(ScreenDC, MoveRect);
  end;
end;
 
procedure TForm1.Panel1MouseUp(Sender: TObject; Button: TMouseButton;
 
  Shift: TShiftState; X, Y: Integer);
begin
 
  if Button = mbLeft then
  begin
    ReleaseCapture;
    DrawFocusRect(ScreenDC, MoveRect);
    Left := Left + X - OldLeft;
    Top := Top + Y - OldTop;
    ReleaseDC(0, ScreenDC);
    Moving := False;
  end;
end;
 
procedure TForm1.Button1Click(Sender: TObject);
var
 
  TitleHeight,
    BorderWidth,
    BorderHeight: Integer;
begin
 
  TitleHeight := GetSystemMetrics(SM_CYCAPTION);
  BorderWidth := GetSystemMetrics(SM_CXBORDER) + GetSystemMetrics(SM_CXFRAME) -
    1;
  BorderHeight := GetSystemMetrics(SM_CYBORDER) + GetSystemMetrics(SM_CYFRAME) -
    2;
  if BorderStyle = bsNone then
  begin
    BorderStyle := bsSizeable;
    Top := Top - TitleHeight - BorderHeight;
    Height := Height + TitleHeight + 2 * BorderHeight;
    Left := Left - BorderWidth;
    Width := Width + 2 * BorderWidth;
  end
  else
  begin
    BorderStyle := bsNone;
    Top := Top + TitleHeight + BorderHeight;
    Height := Height - TitleHeight - 2 * BorderHeight;
    Left := Left + BorderWidth;
    Width := Width - 2 * BorderWidth;
  end;
end;
 
end.
</pre>

<p>Коментарии </p>
<p>У меня есть один коментарий отностительно вышеприведенного кода: данная реализация сложней, чем она должна была быть. Все, что вы должны сделать - это обработать системное сообщение wm_NCHitTest. Я приведу здесь код, который я создал для Borland Tech Info, и который выполняет ту же функцию:</p>
<pre name="code" class="delphi">
unit Dragmain;
 
interface
 
uses
  SysUtils, WinTypes, WinProcs, Messages, Classes, Graphics, Controls,
  Forms, Dialogs, StdCtrls;
 
type
  TForm1 = class(TForm)
    Button1: TButton;
    procedure Button1Click(Sender: TObject);
  private
    procedure WMNCHitTest(var M: TWMNCHitTest); message wm_NCHitTest;
  end;
 
var
 
  Form1: TForm1;
 
implementation
 
{$R *.DFM}
 
procedure TForm1.WMNCHitTest(var M: TWMNCHitTest);
begin
 
  inherited; { вызвали наследованный дескриптор сообщения, }
  if M.Result = htClient then { кликнув в области окна?                     }
    M.Result := htCaption; { если так, то мы заставили Windows думать,   }
  { что это область заголовка.                  }
end;
 
procedure TForm1.Button1Click(Sender: TObject);
begin
  Close;
end;
 
end.
 
 
</pre>
<p><a href="http://delphiworld.narod.ru/" target="_blank">http://delphiworld.narod.ru/</a></p>
<p>DelphiWorld 6.0</p>
&nbsp;</p>
<hr />
<p>Так называемая "плавающая панель" используется обычно для панелей инструментов. </p>
<p>Текст в модуле с основной формой: </p>
<pre name="code" class="delphi">
procedure TForm1.FormShow(Sender: TObject);
begin
  Form2.Show;
end;
</pre>
<p>Текст в модуле с "плавающей" панелью: </p>
<pre name="code" class="delphi">
private
  procedure CreateParams(var Params: TCreateParams); override;
  ...
  procedure TForm2.CreateParams(var Params: TCreateParams);
 
begin
  inherited;
  with Params do
  begin
    Style := Style or WS_OVERLAPPED;
    WndParent := Form1.Handle;
  end;
end;
</pre>
<p><a href="http://delphiworld.narod.ru/" target="_blank">http://delphiworld.narod.ru/</a></p>
<p>DelphiWorld 6.0</p>


</div>
<!-- Actual content end -->
<hr />
<div id="footer">
<p>&copy; DRKB Library, 2010<br />Разработка и поддержка &mdash; <a href="http://www.drkb.ru/" target="_blank">Quadr0</a></p>
</div>
</div>
</body>
</html>
